//
// Copyright 2020, Offchain Labs, Inc.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//    http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//

import type Queue;
import func queue_new() -> Queue;
import func queue_isEmpty(q: Queue) -> bool;
import func queue_get(q: Queue) -> option<(Queue, any)>;
import func queue_put(q: Queue, val: any) -> Queue;

import type MarshalledBytes;

// This is the structure that the Arbitrum protocol gives us, for each incoming message.
// It is declared identically in messages.mini
type MessageFromL1 = struct {
    kind: uint,               // type of message
    blockNumber: uint,        // block number of the L1 block
    timestamp: uint,          // timestamp of the L1 block
    sender: address,          // address of the sender
    inboxSeqNum: uint,        // sequence number in L1 inbox
    msgData: MarshalledBytes  // contents of the message, as a marshalled bytearray
}

type Inbox = struct {
    queue: Queue,     // Queue of messages that haven't yet been retrieved from the inbox
    blockNum: uint,   // Highest blocknum of messages that we have retrieved from the inbox
    timestamp: uint,  // Highest timestamp of messages that we have retrieved from the inbox
}

var globalInbox: Inbox;

public impure func inbox_init() {
    globalInbox = struct {
        queue: queue_new(),
        blockNum: 0,
        timestamp: 0,
    };
}

public impure func inbox_get() -> MessageFromL1 {
    // Get the next message, in order of arrival.
    // If no messages have arrived, this will block until one arrives.

    let inbox = globalInbox;
    while (queue_isEmpty(inbox.queue)) {
        inbox = getFromL1Inbox(inbox);
    }
    let (updatedQ, ret,) = queueGetOrDie(inbox.queue);
    let ebMsg = unsafecast<MessageFromL1>(ret);

    // We got a message. Update the blocknum and timestamp of inbox, if necessary.
    if (inbox.blockNum < ebMsg.blockNumber) {
        inbox = inbox with { blockNum: ebMsg.blockNumber };
    }
    if (inbox.timestamp < ebMsg.timestamp) {
        inbox = inbox with { timestamp: ebMsg.timestamp };
    }

    globalInbox = inbox with { queue: updatedQ };

    return ebMsg;
}

func queueGetOrDie(q: Queue) -> (Queue, any) {
    if let Some(res) = queue_get(q) {
        return res;
    } else {
        panic;
    }
}

// InboxCell is the structure that the Arbitrum protocol uses to combine multiple messages
//     when they arrive in the inbox together.
// This is basically a linked list, but in reverse order (newest message at head of list).
type InboxCell = struct {
    rest: any,   // really an InboxCell, but language doesn't allow recursive types
    msg: any,
}

impure func getFromL1Inbox(inbox: Inbox) -> Inbox {
    // Get one or more messages from the L1 inbox, and append them to the inbox.
    // If there are no messages available in the L1 inbox, this blocks until one is available.
    let newMsgs = asm() InboxCell { inbox };
    inbox = addInboxToQueue(inbox, newMsgs);
    return inbox;
}

func addInboxToQueue(inbox: Inbox, newMsgs: InboxCell) -> Inbox {
    // Unpack messages from an InboxCell list, and append them to an Inbox.
    // Because the messages are in the linked list in newest-first order, we need to reverse the order.
    if (unsafecast<any>(newMsgs) == null) {
        return inbox;
    } else {
        inbox = addInboxToQueue(inbox, unsafecast<InboxCell>(newMsgs.rest));
        let qp = queue_put(inbox.queue, newMsgs.msg);
        return inbox with { queue: qp };
    }
}

public impure func inbox_currentTimestamp() -> uint {
    return globalInbox.timestamp;
}

public impure func inbox_currentBlockNumber() -> uint {
    return globalInbox.blockNum;
}
