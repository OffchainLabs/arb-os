//
// Copyright 2020, Offchain Labs, Inc. All rights reserved.
//

use accounts::AccountStore;
use evmCallStack::EvmCallFrame;
use std::bytearray::ByteArray;

use evmCallStack::EvmCallFrame;
use evmCallStack::evmCallStack_topFrame;
use evmCallStack::evmCallStack_setTopFrameMemory;
use evmCallStack::evmCallFrame_getCalldata;

use evmOps::evmOp_return;
use evmOps::evmOp_revert;
use evmOps::evmOp_revert_knownPc;

use accounts::getGlobalAccountStore;
use accounts::setGlobalAccountStore;
use accounts::accountStore_get;
use accounts::accountStore_set;
use accounts::accountStore_createBuiltinContract;

use pluggables::ModuleFunction;
use pluggables::installPluggable;
use pluggables::bindContractAddressToPluggable;

use std::biguint::biguint_fromBytes;
use std::biguint::biguint_modexp;
use std::biguint::biguint_toBytes;

use std::bytearray::bytearray_new;
use std::bytearray::bytearray_size;
use std::bytearray::bytearray_getByte;
use std::bytearray::bytearray_get256;
use std::bytearray::bytearray_set256;
use std::bytearray::bytearray_extract;
use std::bytearray::bytearray_copy;
use std::bytearray::bytearray_toSizeAndBuffer;
use std::bytearray::bytearray_fromSizeAndBuffer;

use std::sha256::sha256_byteArray;
use std::ripemd160::ripemd160_byteArray;

use arbsys::arbsys_txcall;
use arbowner::arbowner_txcall;
use arbgasinfo::arbgasinfo_txcall;
use arbosTest::arbosTest_txcall;
use arbsys::arbAddressTable_txcall;
use arbsys::arbBLS_txcall;
use arbsys::arbFunctionTable_txcall;
use arbaggregator::arbaggregator_txcall;
use arbretryable::arbretryable_txcall;
use arbstatistics::arbStatistics_txcall;


public impure func precompiles_init(acctStore: AccountStore, isUpgrade: bool) -> option<AccountStore> {
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(0x01),
        makeTrampoline(precompile_0x01),
        false,
        isUpgrade,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(0x02),
        makeTrampoline(precompile_0x02),
        false,
        isUpgrade,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(0x03),
        makeTrampoline(precompile_0x03),
        false,
        isUpgrade,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(0x04),
        makeTrampoline(precompile_0x04),
        false,
        isUpgrade,
    )?;

    installPluggable(
        const::PluggableModuleID_precompile_0x05,
        unsafecast<ModuleFunction>(precompile_0x05_pluggable),
        isUpgrade,
    )?;
    acctStore = accountStore_set(
        acctStore,
        address(0x05),
        bindContractAddressToPluggable(
            accountStore_get(acctStore, address(0x05)),
            address(0x05),
            const::PluggableModuleID_precompile_0x05,
        ),
    );

    acctStore = accountStore_createBuiltinContract(
    	acctStore,
        address(0x06),
        makeTrampoline(precompile_0x06),
        false,
        isUpgrade,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(0x07),
        makeTrampoline(precompile_0x07),
        false,
        isUpgrade,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(0x08),
        makeTrampoline(precompile_0x08),
        false,
        isUpgrade,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(0x09),
        makeTrampoline(precompile_0x09),
        false,
        isUpgrade,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(const::Address_ArbSys),
        makeTrampoline(arbsys_txcall),
        true,
        isUpgrade,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(const::Address_ArbosTest),
        makeTrampoline(arbosTest_txcall),
        true,
        isUpgrade,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(const::Address_ArbAddressTable),
        makeTrampoline(arbAddressTable_txcall),
        true,
        isUpgrade,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(const::Address_ArbBLS),
        makeTrampoline(arbBLS_txcall),
        true,
        isUpgrade,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(const::Address_ArbFunctionTable),
        makeTrampoline(arbFunctionTable_txcall),
        true,
        isUpgrade,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(const::Address_ArbOwner),
        makeTrampoline(arbowner_txcall),
        true,
        isUpgrade,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(const::Address_ArbGasInfo),
        makeTrampoline(arbgasinfo_txcall),
        true,
        isUpgrade,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(const::Address_ArbAggregator),
        makeTrampoline(arbaggregator_txcall),
        true,
        isUpgrade,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(const::Address_ArbRetryableTx),
        makeTrampoline(arbretryable_txcall),
        true,
        isUpgrade,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(const::Address_ArbStatistics),
        makeTrampoline(arbStatistics_txcall),
        true,
        isUpgrade,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(const::Address_ReservedForEthBridge),
        makeTrampoline(revertPrecompile),
        true,
        isUpgrade,
    )?;
    return Some(acctStore);
}

impure func makeTrampoline(txcallFunc: impure func()) -> impure func() {
    // make a small code snippet that function-calls to txcallFunc (with a dummy return address)
    return asm(
        const::AVM_nop,
        0,
        asm(
            const::AVM_jump,
            txcallFunc,
            asm() func() { errcodept }
        ) func() { pushinsnimm }
    ) func() { pushinsnimm };
}

public impure func precompile_0x01() {  // ecrecover
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        if (bytearray_size(calldata) != 4*32) {
            evmOp_revert_knownPc(0, 0, 0);
        }
        let h = bytearray_get256(calldata, 0);
        let v = bytearray_get256(calldata, 32);
        let r = bytearray_get256(calldata, 2*32);
        let s = bytearray_get256(calldata, 3*32);
        let addr = asm(r, s, v-27, h) address { ecrecover };

        let success = evmCallStack_setTopFrameMemory(
            bytearray_set256(bytearray_new(0), 0, uint(addr))
        );
        if (success) {
            evmOp_return(0, 32);
        }
    }

    evmOp_revert_knownPc(0, 0, 0);
}

public impure func precompile_0x02() {  // sha256
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        let result = sha256_byteArray(calldata);
        let success = evmCallStack_setTopFrameMemory(
            bytearray_set256(
            bytearray_new(0), 0, uint(result))
        );
        if (success) {
            evmOp_return(0, 32);
        }
    }

    evmOp_revert_knownPc(0, 0, 0);
}

public impure func precompile_0x03() {  // ripemd160
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        let result = ripemd160_byteArray(calldata);
        let success = evmCallStack_setTopFrameMemory(
            bytearray_set256(bytearray_new(0), 0, uint(result))
        );
        if (success) {
            evmOp_return(0, 32);
        }
    }

    evmOp_revert_knownPc(0, 0, 0);
}

public impure func precompile_0x04() {  // identity function
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        let success = evmCallStack_setTopFrameMemory(calldata);
        if (success) {
            evmOp_return(0, bytearray_size(calldata));
        }
    }

    evmOp_revert_knownPc(0, 0, 0);
}

public impure func precompile_0x05() {  // modexp
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);

        let baseLen = bytearray_get256(calldata, 0);
        let expLen = bytearray_get256(calldata, 32);
        let modLen = bytearray_get256(calldata, 64);

        let base = bytearray_extract(calldata, 96, baseLen);
        let exp = bytearray_extract(calldata, 96+baseLen, expLen);
        let mod = bytearray_extract(calldata, 96+baseLen+expLen, modLen);

        if let Some(result) = biguint_modexp(
            biguint_fromBytes(base),
            biguint_fromBytes(exp),
            biguint_fromBytes(mod),
        ) {
            let mem = biguint_toBytes(result);
            let memSize = bytearray_size(mem);
            if (memSize > modLen) {
                mem = bytearray_extract(mem, memSize-modLen, modLen);
            } elseif (memSize < modLen) {
                mem = bytearray_copy(   // pad result with leading zeroes up to size of modLen
                    mem,
                    0,
                    bytearray_new(0),
                    modLen-memSize,
                    memSize
                );
            }
            if (evmCallStack_setTopFrameMemory(mem)) {
                evmOp_return(0, modLen);
            } else {
                evmOp_revert_knownPc(0, 0, 0);
            }
        } else {
            // modulus was zero, so we return zero
            if (evmCallStack_setTopFrameMemory(bytearray_new(0))) {
                evmOp_return(0, modLen);
            } else {
                evmOp_revert_knownPc(1, 0, 0);
            }
        }
    }

    evmOp_revert_knownPc(2, 0, 0);
}

func precompile_0x05_pluggable(
    args: (EvmCallFrame, ByteArray, any),
    state: any,
    firstTime: bool
) -> (any, any) {  // return ((success: bool, returnData: ByteArray), any)
    let (_, calldata, _) = args;

    let baseLen = bytearray_get256(calldata, 0);
    let expLen = bytearray_get256(calldata, 32);
    let modLen = bytearray_get256(calldata, 64);

    let base = bytearray_extract(calldata, 96, baseLen);
    let exp = bytearray_extract(calldata, 96+baseLen, expLen);
    let mod = bytearray_extract(calldata, 96+baseLen+expLen, modLen);

    return xif let Some(result) = biguint_modexp(
        biguint_fromBytes(base),
        biguint_fromBytes(exp),
        biguint_fromBytes(mod),
    ) {
        let mem = biguint_toBytes(result);
        let memSize = bytearray_size(mem);

        if (memSize > modLen) {
            mem = bytearray_extract(mem, memSize-modLen, modLen);
        } elseif (memSize < modLen) {
            mem = bytearray_copy(   // pad result with leading zeroes up to size of modLen
                mem,
                0,
                bytearray_new(0),
                modLen-memSize,
                memSize
            );
        }
        ((true, mem), ())
    } else {
        ((true, bytearray_extract(bytearray_new(0), 0, modLen)), ())
    };
}

public impure func precompile_0x06() {  // ecadd
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        let x0 = bytearray_get256(calldata, 0);
        let x1 = bytearray_get256(calldata, 32);
        let y0 = bytearray_get256(calldata, 2*32);
        let y1 = bytearray_get256(calldata, 3*32);

        let z0 = asm(x0, x1, y0, y1) uint { ecadd };
        let z1 = asm() uint { };

        let success = evmCallStack_setTopFrameMemory(
            bytearray_set256(
                bytearray_set256(
                    bytearray_new(0),
                    0,
                    z0,
                ),
                32,
                z1,
            )
        );
        if (success) {
            evmOp_return(0, 64);
        }
    }

    evmOp_revert_knownPc(0, 0, 0);
}

public impure func precompile_0x07() {  // ecmul
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        let x0 = bytearray_get256(calldata, 0);
        let x1 = bytearray_get256(calldata, 32);
        let n = bytearray_get256(calldata, 2*32);

        let z0 = asm(x0, x1, n) uint { ecmul };
        let z1 = asm() uint { };

        let success = evmCallStack_setTopFrameMemory(
            bytearray_set256(
                bytearray_set256(
                    bytearray_new(0),
                    0,
                    z0,
                ),
                32,
                z1,
            )
        );
        if (success) {
            evmOp_return(0, 64);
        }
    }

    evmOp_revert_knownPc(0, 0, 0);
}

public impure func precompile_0x08() { // ecpairing
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        let numPts = bytearray_size(calldata) / (6*32);
        if ((numPts >= 0) && (numPts <= 30)) {
            let acc = unsafecast<any>(());
            let offset = 0;
            while (numPts > 0) {
                acc = (
                    (
                        bytearray_get256(calldata, offset),
                        bytearray_get256(calldata, offset+32),
                        bytearray_get256(calldata, offset+3*32),
                        bytearray_get256(calldata, offset+2*32),
                        bytearray_get256(calldata, offset+5*32),
                        bytearray_get256(calldata, offset+4*32)
                    ),
                    acc,
                );
                offset = offset + 6*32;
                numPts = numPts - 1;
            }

            let result = asm(acc,) bool { ecpairing };
            let success = evmCallStack_setTopFrameMemory(
                bytearray_set256(
                    bytearray_new(0),
                    0,
                    uint(result)
                )
            );
            if (success) {
                evmOp_return(0, 32);
            }
        }
    }

    evmOp_revert_knownPc(0, 0, 0);
}

public impure func precompile_0x09() { // Blake2
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        if (bytearray_size(calldata) != 213) {
            evmOp_revert_knownPc(0, 0, 0);
        }

        let numRounds = asm(256-32, bytearray_get256(calldata, 0)) uint { shr };
        if (numRounds > 0xffff) {
            evmOp_revert_knownPc(1, 0, 0);
        }

        let (_, inputBuf) = bytearray_toSizeAndBuffer(calldata);

        if (getbuffer8(inputBuf, 212) > 1) {   // last byte of input must be 0 or 1
            evmOp_revert_knownPc(2, 0, 0);
        }

        let outputBuf = asm(inputBuf,) buffer { blake2f };
        let mem = bytearray_fromSizeAndBuffer(64, outputBuf);
        if (evmCallStack_setTopFrameMemory(mem)) {
            evmOp_return(0, 64);
        } else {
            evmOp_revert_knownPc(3, 0, 0);
        }
    } else {
        evmOp_revert_knownPc(4, 0, 0);
    }
}

public impure func revertPrecompile() {
    evmOp_revert_knownPc(0, 0, 0);
}
