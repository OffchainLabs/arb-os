//
// Copyright 2020, Offchain Labs, Inc. All rights reserved.
//

use retrybuffer::RetryBuffer;
use retrybuffer::retryBuffer_new;
use retrybuffer::retryBuffer_setDefaultLifetime;

use std::bytearray::ByteArray;
use std::bytearray::bytearray_new;
use std::bytearray::bytearray_size;
use std::bytearray::bytearray_setByte;
use std::bytestream::bytestream_new;
use std::bls::BLSKey;
use std::outputbuffer::OutputBuffer;
use std::outputbuffer::outputBuffer_new;
use std::outputbuffer::outputBuffer_appendByte;
use std::outputbuffer::outputBuffer_append256;
use std::keccak::keccak256;
use std::storageMap::StorageMap;
use std::storageMap::storageMap_new;
use std::storageMap::storageMap_size;
use std::storageMap::storageMap_get;
use std::storageMap::storageMap_set;
use std::storageMap::storageMap_diff;
use std::stack::stack_pop;

use core::kvs::Kvs;
use core::kvs::builtin_kvsSize;
use core::kvs::builtin_kvsForall;
use core::kvs::builtin_kvsDelete;

use chainParameters::chainParams_getDefaultAggregator;
use codeSegment::translateEvmCodeSegment;
use decompression::AggregatorDecompressionState;
use evmCallStack::evmCallStack_addStorageCharge;


type AccountStore = struct {
    accounts: map<address, Account>,
    retryBuffer: RetryBuffer,
    numContracts: uint,
    codeRefTable: map<bytes32, CodeRef>,
    escrowStore: EscrowStore,
}

// CodeRefs are reference-counted, but accuracy of the reference count only affects performance, not correctness.
// If we drop a CodeRef too early, the result is that we might recompile some EVM code that we already compiled,
//      and use one more code segment than we needed to.
// If we keep a CodeRef longer than necessary, the result is that we keep state that could have been
//      dropped, so the machine state is larger than it needed to be.
// Operations on the code ref table are designed to be safe, so that nothing bad happens if the refcounts are
//      inaccurate.
type CodeRef = struct {
    refCount: uint,
    code: ByteArray,
    evmJumpTable: map<uint, impure func()>,
    startCodePoint: impure func(),
}

type EscrowStore = struct {
    nextKey: uint,
    escrowedValues: StorageMap,   // amount escrowed under each key; StorageMap is a zero-default uint->uint map
}

type Account = struct {
    addr: address,
    blsKey: option<BLSKey>,
    nextSeqNum: uint,
    ethBalance: uint,
    contractInfo: option<AccountContractInfo>,  // info on the contract at this address, if there is one
    aggregatorInfo: option<AggregatorInfo>,     // info on the aggregator using this address, if there is one
    aggregatorToPay: option<address>,           // aggregator this user prefers; if None, use the default aggregator
}

type AccountContractInfo = struct {
    code: ByteArray,
    evmJumpTable: map<uint, impure func()>,  // maps each valid EVM jump destination to the corresponding AVM codepoint
    startCodePoint: impure func(),
    storage: StorageMap,
}

type AggregatorInfo = struct {
    decompressionState: option<AggregatorDecompressionState>,
    feeCollector: address,    // address to pay this aggregator's fees to (could be, e.g., aggregator's cold wallet)
    baseTxFeeL1Gas: uint,
}

// globalAccountStore is the authoritative source of info on accounts that exist on this chain
// Note that other accountStores will exist, but these others are all contingent somehow,
//       in the sense that they will be written to globalAccountStore only if some event occurs.
// For example, a running transaction will have its own accountStore, which will be written
//       to globalAccountStore only if the transactions succeeds.
var globalAccountStore: AccountStore;   // needs to be initialized before use


public impure func accountStore_init() {   // This must be called first!
    globalAccountStore = struct {
        accounts: newmap<address, Account>,
        retryBuffer: retryBuffer_new(),
        numContracts: 0,
        codeRefTable: newmap<bytes32, CodeRef>,
        escrowStore: escrowStore_new(),
    };
}

public func escrowStore_new() -> EscrowStore {
    return struct {
        nextKey: 1,     // key 0 is pre-reserved for escrowing the current tx's gas funds
        escrowedValues: storageMap_new(),
    };
}

public impure func accounts_notifyParamsChanged() {
    globalAccountStore = globalAccountStore with {
        retryBuffer: retryBuffer_setDefaultLifetime(globalAccountStore.retryBuffer)
    };
}

public impure func getGlobalAccountStore() -> AccountStore {
    return globalAccountStore;
}

public impure func setGlobalAccountStore(acctStore: AccountStore) {
    globalAccountStore = acctStore;
}

public func accountStore_get(acctStore: AccountStore, addr: address) -> Account {
    return xif let Some(acct) = acctStore.accounts[addr] {
        acct
    } else {
        // There is no recorded account at this address, so make a pristine account.
        pristineAccount(addr)
    };
}

public func pristineAccount(addr: address) -> Account {
    return struct {
        addr: addr,
        blsKey: None<BLSKey>,
        nextSeqNum: 0,
        ethBalance: 0,
        contractInfo: None<AccountContractInfo>,
        aggregatorInfo: None<AggregatorInfo>,
        aggregatorToPay: None<address>
    };
}

public func accountStore_set(
    acctStore: AccountStore, 
    addr: address, 
    acct: Account
) -> AccountStore {
    return acctStore with {
        accounts: acctStore.accounts with { [addr] = acct }
    };
}

public func accountStore_getRetryBuffer(acctStore: AccountStore) -> RetryBuffer {
    return acctStore.retryBuffer;
}

public func accountStore_setRetryBuffer(acctStore: AccountStore, rbuf: RetryBuffer) -> AccountStore {
    return acctStore with { retryBuffer: rbuf };
}

public func accountStore_getNumAccounts(acctStore: AccountStore) -> uint {
    return builtin_kvsSize(unsafecast<Kvs>(acctStore.accounts));
}

public func accountStore_getNumContracts(acctStore: AccountStore) -> uint {
    return acctStore.numContracts;
}

public func accountStore_changeNumContracts(acctStore: AccountStore, delta: int) -> AccountStore {
    return acctStore with { numContracts: uint(int(acctStore.numContracts) + delta) };
}

public func accountStore_destroyAccount(acctStore: AccountStore, addrToDestroy: address) -> AccountStore {
    if let Some(contractInfo) = accountStore_get(acctStore, addrToDestroy).contractInfo {
        acctStore = accountStore_dropCodeRef(
            acctStore,
            keccak256(contractInfo.code, 0, bytearray_size(contractInfo.code))
        );
    }
    return accountStore_set(acctStore, addrToDestroy, pristineAccount(addrToDestroy));
}

public func accountStore_destroyAndTransferBalance(
    acctStore: AccountStore,
    victimAddr: address,
    recipientAddr: address
) -> AccountStore {
    // Destroy the account at victimAddr, and give its Eth balance to recipientAddr.
    // If recipientAddr == victimAddr, this burns the funds.

    let victim = accountStore_get(acctStore, recipientAddr);
    let recipient = accountStore_get(acctStore, recipientAddr);
    return xif (victimAddr == recipientAddr) {
        accountStore_set(
            acctStore,
            victimAddr,
            account_addBalance(pristineAccount(victimAddr), victim.ethBalance),
        )
    } else {
        accountStore_set(
            accountStore_set(acctStore, recipientAddr, account_addBalance(recipient, victim.ethBalance)),
            victimAddr,
            pristineAccount(victimAddr),
        )
    };
}

public func account_isEmpty(acct: Account) -> bool {
    return (acct.nextSeqNum == 0) && (acct.ethBalance == 0) && (acct.contractInfo == None<AccountContractInfo>);
}

public func account_hasContract(acct: Account) -> bool {
    return acct.contractInfo != None<AccountContractInfo>;
}

public func account_checkAndIncrSeqNum(
    account: Account, 
    seqNumRecvd: uint
) -> option<Account> {
    // if seqNumRecvd has the expected value, increment seqNum in account and return updated account.
    // else return None
    let nextSeq = account.nextSeqNum;
    return xif (nextSeq == seqNumRecvd) {
        Some(account with { nextSeqNum: nextSeq+1 })
    } else {
        None<Account>
    };
}

public func account_getAddress(account: Account) -> address {
    return account.addr;
}

public func account_getBlsKey(account: Account) -> option<BLSKey> {
    return account.blsKey;
}

public func account_setBlsKey(account: Account, maybeKey: option<BLSKey>) -> Account {
    return account with {
        blsKey: maybeKey
    };
}

public func account_getAggregatorToPayAsOption(account: Account) -> option<address> {
    return account.aggregatorToPay;
}

public impure func account_getAggregatorToPay(account: Account) -> address {
    return xif let Some(atp) = account.aggregatorToPay {
        atp
    } else {
        chainParams_getDefaultAggregator()
    };
}

public func account_setAggregatorToPay(account: Account, atp: option<address>) -> Account {
    return account with { aggregatorToPay: atp };
}

public func account_getStartCodePoint(account: Account) -> option<impure func()> {
    return Some((account.contractInfo?).startCodePoint);
}

public func account_getBalance(account: Account) -> uint {
    return account.ethBalance;
}

public func account_setBalance(account: Account, balance: uint) -> Account {
    return account with { ethBalance: balance };
}

public func account_addBalance(account: Account, balance: uint) -> Account {
    return account with { ethBalance: account.ethBalance + balance };
}

public func account_getEvmJumpTable(account: Account) -> option<map<uint, impure func()>> {
    return Some((account.contractInfo?).evmJumpTable);
}

public func account_fetchAndIncrSeqNum(account: Account) -> (uint, Account) {
    let ret = account.nextSeqNum;
    account = account with {
        nextSeqNum: ret+1
    };
    return (ret, account,);
}

public func account_incrSeqNum(account: Account) -> Account {
    return account with {
        nextSeqNum: account.nextSeqNum + 1
    };
}

public func account_getNextSeqNum(account: Account) -> uint {
    return account.nextSeqNum;
}

public func account_setNextSeqNum(account: Account, num: uint) -> Account {
    return account with { nextSeqNum: num };
}

public func account_getAggregatorDecompressionState(account: Account) -> option<AggregatorDecompressionState> {
    return (account.aggregatorInfo?).decompressionState;
}

public func account_setAggregatorDecompressionState(
    account: Account,
    maybeState: option<AggregatorDecompressionState>,
) -> Account {
    return account with {
        aggregatorInfo: Some(
            xif let Some(aggInfo) = account.aggregatorInfo {
                aggInfo with {
                    decompressionState: maybeState
                }
            } else {
                struct {
                    decompressionState: maybeState,
                    feeCollector: account.addr,
                    baseTxFeeL1Gas: const::Charging_DefaultBaseTxFeeL1Gas,
                }
            }
        )
    };
}

public func accountStore_feeCollectorForAggregator(acctStore: AccountStore, agg: address) -> address {
    return account_getFeeCollector(accountStore_get(acctStore, agg));
}

public func account_getFeeCollector(account: Account) -> address {
    return xif let Some(aggInfo) = account.aggregatorInfo {
        aggInfo.feeCollector
    } else {
        account.addr
    };
}

public func account_setFeeCollector(account: Account, newCollector: address) -> Account {
    return account with {
        aggregatorInfo: Some(
            xif let Some(aggInfo) = account.aggregatorInfo {
                aggInfo with {
                    feeCollector: newCollector
                }
            } else {
                struct {
                    decompressionState: None<AggregatorDecompressionState>,
                    feeCollector: newCollector,
                    baseTxFeeL1Gas: const::Charging_DefaultBaseTxFeeL1Gas,
                }
            }
        )
    };
}

public func account_setBaseTxFeeL1Gas(account: Account, fee: uint) -> Account {
    return account with {
        aggregatorInfo: Some(
            xif let Some(aggInfo) = account.aggregatorInfo {
                aggInfo with {
                    baseTxFeeL1Gas: fee
                }
            } else {
                struct {
                    decompressionState: None<AggregatorDecompressionState>,
                    feeCollector: account.addr,
                    baseTxFeeL1Gas: fee,
                }
            }
        )
    };
}

public impure func accountStore_createAccountFromEvmCode(
    store: AccountStore,
    newAddr: address,
    code: ByteArray,
    evmJumpTable: map<uint, impure func()>,
    initCodePt: impure func(),
    storageMap: StorageMap,
) -> option<AccountStore> {
    // Create a new account for a compiled EVM contract.
    // Returns Some(updatedAccountStore), or None if something went wrong.
    let oldAcct = accountStore_get(store, newAddr);
    return xif ( ! account_hasContract(oldAcct)) {
        evmCallStack_addStorageCharge( (bytearray_size(code) + 31) / 32 );
        Some(accountStore_set(
            store,
            newAddr,
            oldAcct with {
                nextSeqNum: 1
            } with {
                contractInfo: Some(struct {
                    code: code,
                    evmJumpTable: evmJumpTable,
                    startCodePoint: initCodePt,
                    storage: storageMap,
                })
            }
        ))
    } else {
        // There was already an account at that address, so return failure.
        None<AccountStore>
    };
}


public func accountStore_upgradeContractFromEvmCode(
    store: AccountStore,
    addr: address,
    code: ByteArray,
    evmJumpTable: map<uint, impure func()>,
    initCodePt: impure func(),
) -> AccountStore {
    let oldAcct = accountStore_get(store, addr);
    let contractAdded = false;
    store = accountStore_set(
        store,
        addr,
        oldAcct with {
            contractInfo: Some(struct {
                code: code,
                evmJumpTable: evmJumpTable,
                startCodePoint: initCodePt,
                storage: xif let Some(cinfo) = oldAcct.contractInfo {
                    cinfo.storage
                } else {
                    contractAdded = true;
                    storageMap_new()
                },
            })
        }
    );
    return xif (contractAdded) {
        accountStore_changeNumContracts(store, int(1))
    } else {
        store
    };
}

public func accountStore_createBuiltinContract(
    acctStore: AccountStore,
    addr: address,
    entryPoint: impure func(),
    isArbitrumStyle: bool,
    isUpgrade: bool,
) -> option<AccountStore> {
    let oldAcct = accountStore_get(acctStore, addr);

    // set up fake code for the contract
    // if Arbitrum style, must give it nonzero size because callers check if size is zero to see if contract exists
    // otherwise it's an Ethereum precompile which is specified to have null bytearray as its code
    let fakeCode = bytearray_new(0);
    if (isArbitrumStyle) {
        // fake code is a single EVM INVALID instruction
        fakeCode = bytearray_setByte(fakeCode, 0, 0xfe);
    }

    return xif (account_isEmpty(oldAcct) || !isUpgrade) {
        Some(
            accountStore_set(
                acctStore,
                addr,
                oldAcct with {
                    contractInfo: Some(struct {
                        code: fakeCode,
                        evmJumpTable: newmap<uint, impure func()>,
                        startCodePoint: entryPoint,
                        storage: xif (isUpgrade && (oldAcct.contractInfo != None<AccountContractInfo>)) {
                            (oldAcct.contractInfo?).storage
                        } else {
                            storageMap_new()
                        },
                    })
                } with {
                    nextSeqNum: xif (isUpgrade) { oldAcct.nextSeqNum } else { 1 }
                }
            ) with {
                numContracts: acctStore.numContracts + 1
            }
        )
    } elseif (isUpgrade) {
        Some(accountStore_upgradeContractFromEvmCode(
            acctStore,
            addr,
            fakeCode,
            newmap<uint, impure func()>,
            entryPoint,
        ))
    } else {
        // There was already an account at that address, so return failure.
        None<AccountStore>
    };
}

public func accountStore_cloneContract(
    acctStore: AccountStore,
    oldAddr: address, 
    newAddr: address,
    copyStorage: bool,
) -> option<AccountStore> {
    // Clone an existing contract.
    // Return Some(updatedAccountStore), or None if something went wrong.
    let oldAccount = accountStore_get(acctStore, oldAddr);
    let oldContractInfo = oldAccount.contractInfo?;
    let newAccount = oldAccount
        with { ethBalance: 0 }
        with { nextSeqNum: 1 };
    if (!copyStorage) {
        newAccount = newAccount with {
            contractInfo: Some(oldContractInfo with { storage: storageMap_new() })
        };
    }
    return Some(accountStore_set(acctStore, newAddr, newAccount));
}

public func account_getEthBalance(account: Account) -> uint {
    return account.ethBalance;
}

public func account_addToEthBalance(account: Account, addition: uint) -> Account {
    return account with { ethBalance: account.ethBalance + addition };
}

public func account_deductFromEthBalance(account: Account, deduction: uint) -> option<Account> {
    return xif (account.ethBalance >= deduction) {
        Some(
            account with { ethBalance: account.ethBalance-deduction }
        )
    } else {
        None<Account>
    };
}

public func accountStore_transferEthBalance(
    acctStore: AccountStore,
    from: address, 
    to: address, 
    amount: uint
) -> option<AccountStore> {
    // Transfer Eth from one account to another.
    // Return Some(updatedAccountStore), or None if insufficient funds.
    if (amount == 0) {
        return Some(acctStore);
    }

    let fromAcct = accountStore_get(acctStore, from);
    if (fromAcct.ethBalance < amount) {
        return None;
    }
    return Some(
        xif(to == from) {
            acctStore
        } else {
            let toAcct = accountStore_get(acctStore, to);
            accountStore_set(
                accountStore_set(
                    acctStore,
                    from,
                    fromAcct with { ethBalance: fromAcct.ethBalance - amount },
                ),
                to,
                toAcct with { ethBalance: toAcct.ethBalance + amount },
            )
        }
    );
}

public func accountStore_newEscrow(acctStore: AccountStore, payer: address, amount: uint) -> option<(AccountStore, uint)> {
    return xif (amount == 0) {
        Some((acctStore, const::EscrowStoreID_txGas))
    } else {
        let acct = accountStore_get(acctStore, payer);
        xif (acct.ethBalance < amount) {
            None<(AccountStore, uint)>
        } else {
            let key = acctStore.escrowStore.nextKey;
            Some((
                accountStore_set(acctStore, payer, acct with { ethBalance: acct.ethBalance-amount }) with {
                    escrowStore: struct {
                        nextKey: key + 1,
                        escrowedValues: storageMap_set(acctStore.escrowStore.escrowedValues, key, amount),
                    }
                },
                key
            ))
        }
    };
}

public func accountStore_getEscrowBalance(acctStore: AccountStore, key: uint) -> uint {
    return storageMap_get(acctStore.escrowStore.escrowedValues, key);
}

public func accountStore_addToEscrow(acctStore: AccountStore, key: uint, payer: address, amount: uint) -> option<AccountStore> {
    let acct = accountStore_get(acctStore, payer);
    return xif (acct.ethBalance < amount) {
        None<AccountStore>
    } else {
        let oldValue = storageMap_get(acctStore.escrowStore.escrowedValues, key);
        Some(
            accountStore_set(acctStore, payer, acct with { ethBalance: acct.ethBalance-amount }) with {
                escrowStore: acctStore.escrowStore with {
                    escrowedValues: storageMap_set(acctStore.escrowStore.escrowedValues, key, oldValue+amount)
                }
            }
        )
    };
}

public func accountStore_addToEscrowUpToMax(acctStore: AccountStore, key: uint, payer: address, maxAmount: uint) -> (AccountStore, uint) {
    let acct = accountStore_get(acctStore, payer);
    let balance = account_getEthBalance(acct);
    let amount = xif (balance < maxAmount) { balance } else { maxAmount };
    let oldValue = storageMap_get(acctStore.escrowStore.escrowedValues, key);
    return (
        accountStore_set(acctStore, payer, acct with { ethBalance: acct.ethBalance-amount }) with {
            escrowStore: acctStore.escrowStore with {
                escrowedValues: storageMap_set(acctStore.escrowStore.escrowedValues, key, oldValue+amount)
            }
        },
        amount,
    );
}

public func accountStore_payFromEscrow(acctStore: AccountStore, key: uint, recipient: address, amount: uint) -> option<AccountStore> {
    let oldEscrow = storageMap_get(acctStore.escrowStore.escrowedValues, key);
    return xif (oldEscrow < amount) {
        None<AccountStore>
    } else {
        let oldAcct = accountStore_get(acctStore, recipient);
        Some(
            accountStore_set(
                acctStore,
                recipient,
                oldAcct with { ethBalance: oldAcct.ethBalance + amount }
            ) with {
                escrowStore: acctStore.escrowStore with {
                    escrowedValues: storageMap_set(acctStore.escrowStore.escrowedValues, key, oldEscrow-amount)
                }
            }
        )
    };
}

public func accountStore_payFullyFromEscrow(acctStore: AccountStore, key: uint, recipient: address) -> AccountStore {
    let amount = storageMap_get(acctStore.escrowStore.escrowedValues, key);
    let acct = accountStore_get(acctStore, recipient);
    return accountStore_set(
        acctStore,
        recipient,
        acct with { ethBalance: acct.ethBalance + amount }
    ) with {
        escrowStore: acctStore.escrowStore with {
            escrowedValues: storageMap_set(acctStore.escrowStore.escrowedValues, key, 0)
        }
    };
}

public func account_getAllStorage(account: Account) -> option<StorageMap> {
    // Get the full contents of a contract's storage.
    // Return None if account doesn't have contract storage.
    return Some((account.contractInfo?).storage);
}

public func account_getStorageCell(account: Account, cell: uint) -> option<uint> {
    // Get contents of one cell in a contract's storage.
    // Return None if account doesn't have contract storage.
    return Some(storageMap_get((account.contractInfo?).storage, cell));
}

public func account_setStorageCell(
    account: Account, 
    cell: uint, 
    value: uint
) -> option<Account> {
    // Set the value of one cell in a contract's storage.
    // Return None if account doesn't have contract storage.
    let contractInfo = account.contractInfo?;
    return Some(account with {
        contractInfo: Some(contractInfo with {
            storage: storageMap_set(contractInfo.storage, cell, value)
        })
    });
}

public func account_getStorageSize(account: Account) -> option<uint> {
    return Some(storageMap_size((account.contractInfo?).storage));
}

public func account_setContractInfo(
    acct: Account,
    code: ByteArray,
    evmJumpTable: map<uint, impure func()>,
    startCodePoint: impure func(),
    storage: StorageMap,
    forceNonce: bool
) -> Account {
    if (forceNonce && (acct.nextSeqNum == 0)) {
        // new contract accounts are supposed to start with sequence number = 1; make it so
        acct = acct with { nextSeqNum: 1 };
    }

    return acct with { contractInfo: Some(
        struct {
            code: code,
            evmJumpTable: evmJumpTable,
            startCodePoint: startCodePoint,
            storage: storage
        }
    )};
}

public func account_getCodeSize(acct: Account) -> uint {
    return xif let Some(contractInfo) = acct.contractInfo {
        bytearray_size(contractInfo.code)
    } else {
        0  // Account has no code, so its codesize is deemed to be zero.
    };
}

public func account_getCode(acct: Account) -> option<ByteArray> {
    return Some((acct.contractInfo?).code);
}

type asfa_wrappedState = struct {
    innerClosure: func(Account, any) -> any,
    innerState: any,
}

public func accountStore_forall(acctStore: AccountStore, closure: func(Account, any) -> any, startState: any) -> any {
    let wrappedState = struct {
        innerClosure: closure,
        innerState: startState,
    };
    wrappedState = unsafecast<asfa_wrappedState>(builtin_kvsForall(
        unsafecast<Kvs>(acctStore.accounts),
        unsafecast<func(any, any, any) -> any>(asfaClosure),
        wrappedState,
    ));
    return wrappedState.innerState;
}

func asfaClosure(_: any, acct: Account, wrappedState: asfa_wrappedState) -> asfa_wrappedState {
    return wrappedState with {
        innerState: wrappedState.innerClosure(acct, wrappedState.innerState)
    };
}

public func accountStore_codeRefExists(acctStore: AccountStore, codeHash: bytes32) -> bool {
    return acctStore.codeRefTable[codeHash] != None<CodeRef>;
}

public impure func accountStore_createOrAddCodeRef(
    acctStore: AccountStore,
    code: ByteArray
) -> option<(
    AccountStore,
    impure func(),
    map<uint, impure func()>,
    bool,
)> {
    let codeHash = keccak256(code, 0, bytearray_size(code));
    return xif let Some(codeRef) = acctStore.codeRefTable[codeHash] {
        Some((
            accountStore_addCodeRef(acctStore, codeHash)?,
            codeRef.startCodePoint,
            codeRef.evmJumpTable,
            false,
        ))
    } else {
        let (startCodePoint, evmJumpTable, _) = translateEvmCodeSegment(bytestream_new(code), false);
        Some((
            accountStore_createCodeRef(acctStore, code, evmJumpTable, startCodePoint),
            unsafecast<impure func()>(startCodePoint),
            unsafecast<map<uint, impure func()>>(evmJumpTable),
            true,
        ))
    };
}

public func accountStore_addCodeRef(acctStore: AccountStore, codeHash: bytes32) -> option<AccountStore> {
    // returns None if coderef doesn't exist

    let codeRef = acctStore.codeRefTable[codeHash]?;
    return Some(
        acctStore with {
            codeRefTable: acctStore.codeRefTable with {
                [codeHash] = codeRef with {
                    refCount: codeRef.refCount + 1
                }
            }
        }
    );
}

public func accountStore_createCodeRef(
    acctStore: AccountStore,
    code: ByteArray,
    evmJumpTable: map<uint, impure func()>,
    startCodePoint: impure func(),
) -> AccountStore {
    let codeHash = keccak256(code, 0, bytearray_size(code));
    return acctStore with {
        codeRefTable: acctStore.codeRefTable with {
            [codeHash] = struct {
                refCount: 1,
                code: code,
                evmJumpTable: evmJumpTable,
                startCodePoint: startCodePoint,
            }
        }
    };
}

public func accountStore_dropCodeRef(acctStore: AccountStore, codeHash: bytes32) -> AccountStore {
    // this is a no-op if coderef doesn't exist

    return xif let Some(codeRef) = acctStore.codeRefTable[codeHash] {
        acctStore with {
            codeRefTable: xif (codeRef.refCount == 1) {
                unsafecast<map<bytes32, CodeRef>>(
                    builtin_kvsDelete(unsafecast<Kvs>(acctStore.codeRefTable), uint(codeHash))
                )
            } else {
                acctStore.codeRefTable with {
                    [codeHash] = codeRef with {
                        refCount: codeRef.refCount - 1
                    }
                }
            }
        }
    } else {
        acctStore
    };
}
