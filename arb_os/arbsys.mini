//
// Copyright 2020, Offchain Labs, Inc. All rights reserved.
//

use evmCallStack::EvmCallFrame;
use accounts::Account;
use std::bytearray::ByteArray;
use std::bytearray::MarshalledBytes;
use std::stack::Stack;
use accounts::AccountStore;
use std::bytestream::ByteStream;

use evmCallStack::evmCallStack_stackDepth;
use evmCallStack::evmCallStack_topFrame;
use evmCallStack::evmCallStack_setAccount;
use evmCallStack::evmCallStack_getTopFrameMemoryOrDie;
use evmCallStack::evmCallStack_setTopFrameMemory;
use evmCallStack::evmCallStack_callHitError;
use evmCallStack::evmCallStack_addEvmLogToCurrent;
use evmCallStack::evmCallStack_addStorageCharge;

use evmCallStack::evmCallFrame_getCalldata;
use evmCallStack::evmCallFrame_getCallvalue;
use evmCallStack::evmCallFrame_getAccount;
use evmCallStack::evmCallFrame_runningAsAddress;
use evmCallStack::evmCallFrame_runningAsAccount;
use evmCallStack::evmCallFrame_getParent;
use evmCallStack::evmCallFrame_getCaller;

use evmOps::evmOp_return;
use evmOps::evmOp_revert_knownCodePc;

use accounts::account_getAddress;
use accounts::account_getNextSeqNum;
use accounts::account_getStorageCell;
use accounts::account_deductFromEthBalance;
use accounts::account_setBlsKey;
use accounts::account_getBlsKey;
use accounts::account_getAggregatorDecompressionState;
use accounts::account_setAggregatorDecompressionState;

use std::bytearray::bytearray_new;
use std::bytearray::bytearray_size;
use std::bytearray::bytearray_getByte;
use std::bytearray::bytearray_get256;
use std::bytearray::bytearray_set256;
use std::bytearray::bytearray_extract;
use std::bytearray::bytearray_extract;
use std::bytearray::bytearray_copy;

use std::bytestream::bytestream_new;
use std::bytestream::bytestream_bytesReadSoFar;

use inbox::inbox_currentArbBlockNumber;
use inbox::inbox_currentEthBlockNumber;
use inbox::inbox_currentTimestamp;

use std::stack::stack_pop;

use output::queueMessageForSend;

use decompression::getGlobalAddressTable;
use decompression::setGlobalAddressTable;
use decompression::addressTable_size;
use decompression::addressTable_getByAddressAlloc;
use decompression::addressTable_getByAddress;
use decompression::addressTable_getByIndex;
use decompression::decompressAddress;
use decompression::getFromFunctionTable;
use decompression::functionTableSize;
use decompression::getFromFunctionTable;

use std::rlp::rlp_encodeUint;
use std::rlp::rlp_encodeAddress;

use std::bls::bls_makeKey;
use std::bls::bls_marshalPublicKey;

use decompression::parseAggregatorFunctionTable;

use codeSegment::pcTableForCode;


public impure func arbsys_txcall() {
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        if (bytearray_size(calldata) < 4) {
            evmOp_revert_knownCodePc(address(const::Address_ArbSys), 0, 0, 0);
        }
        let funcCode = getFuncCode(calldata);
        if (funcCode == 0x051038F2) {
            arbsys_arbOSVersion(topFrame, calldata);
        } elseif(funcCode == 0xa3b1b31d) {
            arbsys_arbBlockNumber(topFrame, calldata);
        } elseif(funcCode == 0x25e16063) {
            arbsys_withdrawEth(topFrame, calldata);
        } elseif(funcCode == 0x928c169a) {
            arbsys_sendTxToL1(topFrame, calldata);
        } elseif(funcCode == 0x23ca0cd2) {
            arbsys_getTransactionCount(topFrame, calldata);
        } elseif(funcCode == 0xa169625f) {
            arbsys_getStorageAt(topFrame, calldata);
        } elseif(funcCode == 0x3dcfdac7) {
            arbsys_getPcTable(topFrame, calldata);
        } elseif (funcCode == 0x08bd624c) {
            arbsys_isTopLevelCall(topFrame, calldata);
        } else {
            // unrecognized function code
            evmOp_revert_knownCodePc(address(const::Address_ArbSys), 1, 0, 0);
        }
    } else {
        // this shouldn't happen -- should always be called in an EVM tx
        evmCallStack_callHitError(21);
    }
}

public impure func arbAddressTable_txcall() {
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        if (bytearray_size(calldata) < 4) {
            evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 0, 0, 0);
        }
        let funcCode = getFuncCode(calldata);

        if(funcCode == 0x4420e486) {
            arbAddressTable_register(topFrame, calldata);
        } elseif(funcCode == 0xd4b6b5da) {
            arbAddressTable_lookup(topFrame, calldata);
        } elseif(funcCode == 0xa5025222) {
            arbAddressTable_addressExists(topFrame, calldata);
        } elseif(funcCode == 0x949d225d) {
            arbAddressTable_size(topFrame, calldata);
        } elseif(funcCode == 0x8a186788) {
            arbAddressTable_lookupIndex(topFrame, calldata);
        } elseif(funcCode == 0x31862ada) {
            arbAddressTable_decompress(topFrame, calldata);
        } elseif(funcCode == 0xf6a455a2) {
            arbAddressTable_compress(topFrame, calldata);
        } else {
            // unrecognized function code
            evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 1, 0, 0);
        }
    } else {
        // this shouldn't happen -- should always be called in an EVM tx
        evmCallStack_callHitError(17);
    }
}

public impure func arbBLS_txcall() {
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        if (bytearray_size(calldata) < 4) {
            evmOp_revert_knownCodePc(address(const::Address_ArbBLS), 0, 0, 0);
        }
        let funcCode = getFuncCode(calldata);
        if (funcCode == 0x375a7c7f) {
            arbBLS_register(topFrame, calldata);
        } elseif(funcCode == 0x857cdbb8) {
            arbBLS_getPublicKey(topFrame, calldata);
        } else {
            // unrecognized function code
            evmOp_revert_knownCodePc(address(const::Address_ArbBLS), 1, 0, 0);
        }
    } else {
        // this shouldn't happen -- should always be called in an EVM tx
        evmCallStack_callHitError(18);
    }
}

public impure func arbFunctionTable_txcall() {
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        if (bytearray_size(calldata) < 4) {
            evmOp_revert_knownCodePc(address(const::Address_ArbFunctionTable), 0, 0, 0);
        }
        let funcCode = getFuncCode(calldata);
        if (funcCode == 0xce2ae159) {
            arbFunctionTable_upload(topFrame, calldata);
        } elseif(funcCode == 0x88987068) {
            arbFunctionTable_size(topFrame, calldata);
        } elseif(funcCode == 0xb464631b) {
            arbFunctionTable_get(topFrame, calldata);
        } else {
            // unrecognized function code
            evmOp_revert_knownCodePc(address(const::Address_ArbFunctionTable), 1, 0, 0);
        }
    } else {
        // this shouldn't happen -- should always be called in an EVM tx
        evmCallStack_callHitError(18);
    }
}

func getFuncCode(ba: ByteArray) -> uint {
    return asm(224, bytearray_get256(ba, 0)) uint { shr };
}

public func arbosVersion() -> uint {
    return 1;
}

impure func arbsys_arbOSVersion(topFrame: EvmCallFrame, calldata: ByteArray) {  // () -> uint
    if (bytearray_size(calldata) != 4) {
        evmOp_revert_knownCodePc(address(const::Address_ArbSys), 5, 0, 0);
    }
    let version = arbosVersion();
    if (evmCallStack_setTopFrameMemory(bytearray_set256(bytearray_new(0), 0, version))) {
        evmOp_return(0, 32);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbSys), 6, 0, 0);
    }
}

impure func arbsys_arbBlockNumber(topFrame: EvmCallFrame, calldata: ByteArray) {  // () -> uint
    if (bytearray_size(calldata) != 4) {
        evmOp_revert_knownCodePc(address(const::Address_ArbSys), 6, 0, 0);
    }
    let blockNum = inbox_currentArbBlockNumber();
    if (evmCallStack_setTopFrameMemory(bytearray_set256(bytearray_new(0), 0, blockNum))) {
        evmOp_return(0, 32);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbSys), 7, 0, 0);
    }
}

impure func arbsys_withdrawEth(topFrame: EvmCallFrame, calldata: ByteArray) { // (address payee) payable -> nil
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbSys), 30, 0, 0);
    }
    let senderAddr = evmCallFrame_getCaller(topFrame);
    let destAddr = address(bytearray_get256(calldata, 4));
    let amount = evmCallFrame_getCallvalue(topFrame);

    let runningAsAddr = evmCallFrame_runningAsAddress(topFrame);
    let runningAsAccount = evmCallFrame_getAccount(topFrame, runningAsAddr);
    if let Some(updatedAcct) = account_deductFromEthBalance(runningAsAccount, amount) {
        if ( ! evmCallStack_setAccount(runningAsAddr, updatedAcct)) {
            evmOp_revert_knownCodePc(address(const::Address_ArbSys), 31, 0, 0);
        }
        queueMessageForSend(
            bytearray_set256(
                bytearray_set256(
                    bytearray_set256(bytearray_new(0), 0, const::SendType_withdrawETH),
                    32,
                    bytearray_get256(calldata, 4),
                ),
                64,
                amount
            )
        );
        if (evmCallStack_addEvmLogToCurrent(
            (
                address(const::Address_ArbSys),  // addEvmLogToCurrent will fill this in
                (32, setbuffer256(newbuffer(), 0, amount)),
                0xc32d3c7eb0f275cbb5b72b3d3c688269430f30e5b9bb36980396edd9101c615c, // EthWithdrawal(address,uint256)
                uint(destAddr),
            )
        ) == Some(())) {
            evmOp_return(0, 0);
        }
    }

    evmOp_revert_knownCodePc(address(const::Address_ArbSys), 32, 0, 0);
}

impure func arbsys_sendTxToL1(topFrame: EvmCallFrame, calldata: ByteArray) { // (address, bytes) payable -> bool
    let destAddr = address(bytearray_get256(calldata, 4));
    let nbytes = bytearray_get256(calldata, 4+2*32);
    let innerCalldata = bytearray_extract(calldata, 4+3*32, nbytes);

    let callvalue = evmCallFrame_getCallvalue(topFrame);
    let runningAsAddr = evmCallFrame_runningAsAddress(topFrame);
    let runningAsAccount = evmCallFrame_getAccount(topFrame, runningAsAddr);
    if let Some(updatedAcct) = account_deductFromEthBalance(runningAsAccount, callvalue) {
        if ( ! evmCallStack_setAccount(runningAsAddr, updatedAcct)) {
            evmOp_revert_knownCodePc(address(const::Address_ArbSys), 220, 0, 0);
        }
        queueMessageForSend(
            bytearray_copy(
                innerCalldata,
                0,
                bytearray_set256(
                    bytearray_set256(
                        bytearray_set256(
                            bytearray_set256(
                                bytearray_set256(
                                    bytearray_set256(
                                        bytearray_set256(
                                            bytearray_new(0),
                                            0,
                                            const::SendType_sendTxToL1
                                        ),
                                        32,
                                        uint(evmCallFrame_getCaller(topFrame)),
                                    ),
                                    64,
                                    uint(destAddr),
                                ),
                                96,
                                inbox_currentArbBlockNumber(),
                            ),
                            128,
                            inbox_currentEthBlockNumber(),
                        ),
                        160,
                        inbox_currentTimestamp(),
                    ),
                    192,
                    callvalue,
                ),
                224,
                nbytes
            ),
        );
        if (evmCallStack_setTopFrameMemory(bytearray_set256(bytearray_new(0), 0, 1))) {
            evmOp_return(0, 32);
        } else {
            evmOp_revert_knownCodePc(address(const::Address_ArbSys), 221, 0, 0);
        }
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbSys), 222, 0, 0);
    }
}

impure func arbsys_getTransactionCount(topFrame: EvmCallFrame, calldata: ByteArray) { // (address account) -> uint
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbSys), 40, 0, 0);
    }
    let addr = address(bytearray_get256(calldata, 4));
    let seqNum = account_getNextSeqNum(evmCallFrame_getAccount(topFrame, addr));
    let updatedMem = bytearray_set256(
         evmCallStack_getTopFrameMemoryOrDie(),
         0,
         seqNum
     );
    let success = evmCallStack_setTopFrameMemory(updatedMem);
    if (success) {
        evmOp_return(0, 32);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbSys), 41, 0, 0);
    }
}

impure func arbsys_getStorageAt(topFrame: EvmCallFrame, calldata: ByteArray) {
    let senderAddr = evmCallFrame_getCaller(topFrame);
    if (senderAddr != address(0)) {
        evmOp_revert_knownCodePc(address(const::Address_ArbSys), 50, 0, 0);
    }

    let addr = address(bytearray_get256(calldata, 4));
    let offset = bytearray_get256(calldata, 4+32);
    let acct = evmCallFrame_getAccount(topFrame, addr);

    let ret = 0;
    if let Some(val) = account_getStorageCell(acct, offset) {
        ret = val;
    }

    let updatedMem = bytearray_set256(evmCallStack_getTopFrameMemoryOrDie(), 0, ret);
    let success = evmCallStack_setTopFrameMemory(updatedMem);

    if (success) {
        evmOp_return(0, 32);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbSys), 51, 0, 0);
    }
}

impure func arbsys_getPcTable(topFrame: EvmCallFrame, calldata: ByteArray) {
    if (bytearray_size(calldata) <= 4) {
        evmOp_revert_knownCodePc(address(const::Address_ArbSys), 60, 0, 0);
    }
    if let Some(pcTable) = pcTableForCode(bytearray_extract(calldata, 4, bytearray_size(calldata)-4)) {
        let mem = evmCallStack_getTopFrameMemoryOrDie();
        mem = bytearray_set256(mem, 0, 64);
        let offset = 64;
        loop {
            if let Some(res) = stack_pop(pcTable) {
                let (upct, item) = res;
                pcTable = upct;
                let (evmPc, avmCpHash) = unsafecast<(uint, bytes32)>(item);
                mem = bytearray_set256(
                    bytearray_set256(mem, offset, evmPc),
                    offset+32,
                    uint(avmCpHash),
                );
                offset = offset + 64;
            } else {
                // done reading data, finish building returndata then return
                mem = bytearray_set256(mem, 32, (offset-64)/32);
                evmOp_return(0, offset);
            }
        }
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbSys), 61, 0, 0);
    }
}

impure func arbsys_isTopLevelCall(topFrame: EvmCallFrame, calldata: ByteArray) {   // () -> bool
    if (bytearray_size(calldata) != 4) {
        evmOp_revert_knownCodePc(address(const::Address_ArbSys), 70, 0, 0);
    }
    let mem = bytearray_set256(
        bytearray_new(0),
        0,
        uint(evmCallStack_stackDepth() == 2),
    );
    if (evmCallStack_setTopFrameMemory(mem)) {
        evmOp_return(0, 32);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbSys), 71, 0, 0);
    }
}

impure func arbAddressTable_register(topFrame: EvmCallFrame, calldata: ByteArray) { // (address addr) -> uint
// return the index of addr in the address table
// if addr isn't in the table already, add it to the table and return its new index
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 110, 0, 0);
    }
    let addr = address(bytearray_get256(calldata, 4));
    let (atab, idx, changed) = addressTable_getByAddressAlloc(getGlobalAddressTable(), addr);
    if (changed) {
        evmCallStack_addStorageCharge(2);   // charge current tx for 2 storage cells
        setGlobalAddressTable(atab);
    }
    if (evmCallStack_setTopFrameMemory(bytearray_set256(evmCallStack_getTopFrameMemoryOrDie(), 0, idx))) {
        evmOp_return(0, 32);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 111, 0, 0);
    }
}

impure func arbAddressTable_lookup(topFrame: EvmCallFrame, calldata: ByteArray) {  // (address addr) -> uint
// return the index of addr in the address table; revert if it's not in the table
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 120, 0, 0);
    }
    let addr = address(bytearray_get256(calldata, 4));
    if let Some(idx) = addressTable_getByAddress(getGlobalAddressTable(), addr) {
        if (evmCallStack_setTopFrameMemory(bytearray_set256(evmCallStack_getTopFrameMemoryOrDie(), 0, idx))) {
            evmOp_return(0, 32);
        } else {
            evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 121, 0, 0);
        }
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 122, 0, 0);
    }
}

impure func arbAddressTable_addressExists(topFrame: EvmCallFrame, calldata: ByteArray) { // (address addr) -> bool
// return true iff addr is in the address table
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 130, 0, 0);
    }
    let addr = address(bytearray_get256(calldata, 4));
    let inTable = 0;
    if let Some(_) = addressTable_getByAddress(getGlobalAddressTable(), addr) {
        inTable = 1;
    }

    if (evmCallStack_setTopFrameMemory(bytearray_set256(evmCallStack_getTopFrameMemoryOrDie(), 0, inTable))) {
        evmOp_return(0, 32);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 131, 0, 0);
    }
}

impure func arbAddressTable_size(topFrame: EvmCallFrame, calldata: ByteArray) { // () -> uint
// return the number of items in the address table
    if (bytearray_size(calldata) != 4) {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 140, 0, 0);
    }
    let size = addressTable_size(getGlobalAddressTable());
    if (evmCallStack_setTopFrameMemory(bytearray_set256(evmCallStack_getTopFrameMemoryOrDie(), 0, size))) {
        evmOp_return(0, 32);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 141, 0, 0);
    }
}

impure func arbAddressTable_lookupIndex(topFrame: EvmCallFrame, calldata: ByteArray) {  // (uint index) -> address
// return the address at slot index in the address table, or revert if index is beyond the end of the table
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 150, 0, 0);
    }
    let index = bytearray_get256(calldata, 4);
    if let Some(addr) = addressTable_getByIndex(getGlobalAddressTable(), index) {
        if (evmCallStack_setTopFrameMemory(bytearray_set256(evmCallStack_getTopFrameMemoryOrDie(), 0, uint(addr)))) {
            evmOp_return(0, 32);
        } else {
            evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 151, 0, 0);
        }
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 152, 0, 0);
    }
}

impure func arbAddressTable_decompress(topFrame: EvmCallFrame, calldata: ByteArray) {  // (bytes buf, uint offset) -> (address, uint)
    // read a compressed address from buf at offset, return (resulting address, updated offset)
    // revert if buf is too short
    if (bytearray_size(calldata) < 4+4*32) {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 160, 0, 0);
    }
    let offset = bytearray_get256(calldata, 36);
    let bytesLen = bytearray_get256(calldata, 68);
    if (offset >= bytesLen) {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 161, 0, 0);  // buffer is too short
    }
    let stream = bytestream_new(bytearray_extract(calldata, 100+offset, bytesLen-offset));
    if let Some(res) = decompressAddress(stream) {
        let (ustr, addr) = res;
        stream = ustr;
        if (evmCallStack_setTopFrameMemory(
            bytearray_set256(
                bytearray_set256(
                    evmCallStack_getTopFrameMemoryOrDie(),
                    0,
                    uint(addr)
                ),
                32,
                offset + bytestream_bytesReadSoFar(stream),
            )
        )) {
            evmOp_return(0, 64);
        } else {
            evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 162, 0, 0);
        }
    } else {
        // input was too short
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 163, 0, 0);
    }
}

impure func arbAddressTable_compress(topFrame: EvmCallFrame, calldata: ByteArray) { // (address addr) -> bytes
// compress addr, based on the current address table contents, and return the resulting buffer
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 100, 0, 0);
    }
    let addr = address(bytearray_get256(calldata, 4));
    let compressedData = bytearray_new(0);
    let compressedDataLen = 0;
    if let Some(index) = addressTable_getByAddress(getGlobalAddressTable(), addr) {
        let (ba, sz) = rlp_encodeUint(index, compressedData, 0);
        compressedData = ba;
        compressedDataLen = sz;
    } else {
        let (ba, sz) = rlp_encodeAddress(addr, compressedData, 0);
        compressedData = ba;
        compressedDataLen = sz;
    }
    let mem = bytearray_set256(
        bytearray_set256(
            bytearray_set256(
                evmCallStack_getTopFrameMemoryOrDie(),
                0,
                32
            ),
            32,
            compressedDataLen
        ),
        64,
        bytearray_get256(compressedData, 0)
    );

    if (evmCallStack_setTopFrameMemory(mem)) {
        evmOp_return(0, 96);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 101, 0, 0);
    }
}

impure func arbBLS_register(topFrame: EvmCallFrame, calldata: ByteArray) {
    if (bytearray_size(calldata) != 4+32*4) {
        evmOp_revert_knownCodePc(address(const::Address_ArbBLS), 170, 0, 0);
    }

    let x0 = bytearray_get256(calldata, 4);
    let x1 = bytearray_get256(calldata, 4+32);
    let y0 = bytearray_get256(calldata, 4+32*2);
    let y1 = bytearray_get256(calldata, 4+32*3);
    let maybeBlsKey = bls_makeKey(x0, x1, y0, y1);

    let caller = evmCallFrame_getCaller(topFrame);
    let acct = evmCallFrame_getAccount(topFrame, caller);
    if (evmCallStack_setAccount(caller, account_setBlsKey(acct, maybeBlsKey))) {
        evmOp_return(0, 0);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbBLS), 171, 0, 0);
    }
}

impure func arbBLS_getPublicKey(topFrame: EvmCallFrame, calldata: ByteArray) { // (address) -> (uint, uint, uint, uint)
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbBLS), 180, 0, 0);
    }
    let addr = address(bytearray_get256(calldata, 4));
    let maybeBlsKey = account_getBlsKey(evmCallFrame_getAccount(topFrame, addr));

    let mem = evmCallStack_getTopFrameMemoryOrDie();
    if let Some(blsKey) = maybeBlsKey {
        mem = bls_marshalPublicKey(blsKey, mem, 0);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbBLS), 101, 0, 0);
    }

    if (evmCallStack_setTopFrameMemory(mem)) {
        evmOp_return(0, 4*32);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbBLS), 182, 0, 0);
    }
}

impure func arbFunctionTable_upload(topFrame: EvmCallFrame, calldata: ByteArray) { // (bytes)
    if (bytearray_size(calldata) < (4+2*32)) {
        evmOp_revert_knownCodePc(address(const::Address_ArbFunctionTable), 190, 0, 0);
    }

    let callerAddr = evmCallFrame_getCaller(topFrame);
    let nbytes = bytearray_get256(calldata, 4+32);

    let tableData = bytearray_extract(calldata, 4+2*32, nbytes);

    let account = evmCallFrame_getAccount(topFrame, callerAddr);
    if let Some(funcTable) = parseAggregatorFunctionTable(
        account_getAggregatorDecompressionState(account),
        tableData,
    ) {
        account = account_setAggregatorDecompressionState(
            account,
            Some(funcTable)
        );
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbFunctionTable), 191, 0, 0);
    }

    if (evmCallStack_setAccount(callerAddr, account)) {
        evmOp_return(0, 0);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbFunctionTable), 192, 0, 0);
    }
}

impure func arbFunctionTable_size(topFrame: EvmCallFrame, calldata: ByteArray) { // (address) -> uint
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbFunctionTable), 200, 0, 0);
    }

    if let Some(size) = functionTableSize(address(bytearray_get256(calldata, 4))) {
        if (evmCallStack_setTopFrameMemory(bytearray_set256(bytearray_new(0), 0, size))) {
            evmOp_return(0, 32);
        }
    }

        evmOp_revert_knownCodePc(address(const::Address_ArbFunctionTable), 201, 0, 0);
}

impure func arbFunctionTable_get(topFrame: EvmCallFrame, calldata: ByteArray) { // (address, uint) -> (uint, bool, uint)
    if (bytearray_size(calldata) != 68) {
        evmOp_revert_knownCodePc(address(const::Address_ArbFunctionTable), 210, 0, 0);
    }

    let addr = address(bytearray_get256(calldata, 4));
    let index = bytearray_get256(calldata, 36);

    if let Some(res) = getFromFunctionTable(addr, index) {
        let (funcCode, isPayable, gasLimit) = res;
        if (evmCallStack_setTopFrameMemory(
            bytearray_set256(
                bytearray_set256(
                    bytearray_set256(
                        bytearray_new(0),
                        0,
                        funcCode
                    ),
                    32,
                    uint(isPayable),
                ),
                64,
                gasLimit,
            )
        )) {
            evmOp_return(0, 96);
        }
    }

    evmOp_revert_knownCodePc(address(const::Address_ArbFunctionTable), 211, 0, 0);
}

