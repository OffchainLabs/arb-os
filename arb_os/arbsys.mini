//
// Copyright 2020, Offchain Labs, Inc. All rights reserved.
//

import type EvmCallFrame;
import type Account;
import type ByteArray;
import type MarshalledBytes;

import impure func evmCallStack_topFrame() -> option<EvmCallFrame>;
import impure func evmCallStack_setAccount(addr: address, acct: Account) -> bool;
import impure func evmCallStack_getTopFrameMemoryOrDie() -> ByteArray;
import impure func evmCallStack_setTopFrameMemory(mem: ByteArray) -> bool;
import impure func evmCallStack_cloneContract(addr: address) -> option<address>;
import func evmCallStack_callHitError(reason: uint);

import func evmCallFrame_getCalldata(frame: EvmCallFrame) -> ByteArray;
import func evmCallFrame_getCallvalue(frame: EvmCallFrame) -> uint;
import func evmCallFrame_getAccount(frame: EvmCallFrame, addr: address) -> Account;
import func evmCallFrame_runningAsAddress(frame: EvmCallFrame) -> address;
import func evmCallFrame_runningAsAccount(frame: EvmCallFrame) -> Account;
import func evmCallFrame_getParent(frame: EvmCallFrame) -> option<EvmCallFrame>;
import func evmCallFrame_getCaller(frame: EvmCallFrame) -> address;

import impure func evmOp_return(memOffset: uint, memNbytes: uint);
import impure func evmOp_revert_knownCodePc(codeAddr: address, evmPc: uint, memOffset: uint, memNbytes: uint);

import func account_getAddress(acct: Account) -> address;
import func account_getNextSeqNum(account: Account) -> uint;
import func account_getStorageCell(account: Account, cell: uint) -> option<uint>;
import func account_deductFromEthBalance(acct: Account, amount: uint) -> option<Account>;

import func bytearray_size(ba: ByteArray) -> uint;
import func bytearray_getByte(ba: ByteArray, offset: uint) -> uint;
import func bytearray_get256(ba: ByteArray, offset: uint) -> uint;
import func bytearray_set256(ba: ByteArray, offset: uint, value: uint) -> ByteArray;
import func bytearray_extract(ba: ByteArray, offset: uint, nbytes: uint) -> ByteArray;
import func bytearray_marshalFull(ba: ByteArray) -> MarshalledBytes;

import impure func inbox_currentBlockNumber() -> uint;
import impure func inbox_currentTimestamp() -> uint;

import impure func queueMessageForSend(msgType: uint, sender: address, data: ByteArray);


public impure func arbsys_txcall() {
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        if (bytearray_size(calldata) < 4) {
            evmOp_revert_knownCodePc(address(100), 0, 0, 0);
        }
        let funcCode = getFuncCode(calldata);
        if (funcCode == 0xa1db9782) {
            arbsys_withdrawErc20(topFrame, calldata);
        } elseif(funcCode == 0xf3e414f8) {
            arbsys_withdrawErc721(topFrame, calldata);
        } elseif(funcCode == 0x25e16063) {
            arbsys_withdrawEth(topFrame, calldata);
        } elseif(funcCode == 0x23ca0cd2) {
            arbsys_getTransactionCount(topFrame, calldata);
        } elseif(funcCode == 0x474ed9c0) {
            arbsys_cloneContract(topFrame, calldata);
        } elseif(funcCode == 0xa169625f) {
            arbsys_getStorageAt(topFrame, calldata);
        } else {
            // unrecognized function code
            evmOp_revert_knownCodePc(address(100), 0, 0, 0);
        }
    } else {
        // this shouldn't happen -- should always be called in an EVM tx
        evmCallStack_callHitError(14);
    }
}

func getFuncCode(ba: ByteArray) -> uint {
    return asm(224, bytearray_get256(ba, 0)) uint { shr };
}

impure func arbsys_withdrawErc20(topFrame: EvmCallFrame, calldata: ByteArray) {
    if (bytearray_size(calldata) != 68) {
        evmOp_revert_knownCodePc(address(100), 0, 0, 0);
    }
    let senderAcct = evmCallFrame_runningAsAccount(topFrame);
    let senderAddr = account_getAddress(senderAcct);
    let destAddr = address(bytearray_get256(calldata, 4));
    let amount = bytearray_get256(calldata, 36);

    let messageData = bytearray_extract(calldata, 4, 64);
    queueMessageForSend(1, senderAddr, messageData);
    evmOp_return(0, 0);
}

impure func arbsys_withdrawErc721(topFrame: EvmCallFrame, calldata: ByteArray) {
    if (bytearray_size(calldata) != 68) {
        evmOp_revert_knownCodePc(address(100), 0, 0, 0);
    }
    let senderAcct = evmCallFrame_runningAsAccount(topFrame);
    let senderAddr = account_getAddress(senderAcct);
    let destAddr = address(bytearray_get256(calldata, 4));
    let id = bytearray_get256(calldata, 36);

    let messageData = bytearray_extract(calldata, 4, 64);
    queueMessageForSend(2, senderAddr, messageData);
    evmOp_return(0, 0);
}

impure func arbsys_withdrawEth(topFrame: EvmCallFrame, calldata: ByteArray) {
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(100), 0, 0, 0);
    }
    let senderAddr = evmCallFrame_getCaller(topFrame);
    let senderAcct = evmCallFrame_getAccount(topFrame, senderAddr);
    let destAddr = address(bytearray_get256(calldata, 4));
    let amount = evmCallFrame_getCallvalue(topFrame);

    let arbSysAddr = evmCallFrame_runningAsAddress(topFrame);
    let arbSysAccount = evmCallFrame_getAccount(topFrame, arbSysAddr);
    if let Some(updatedAcct) = account_deductFromEthBalance(arbSysAccount, amount) {
        if ( ! evmCallStack_setAccount(arbSysAddr, updatedAcct)) {
            evmOp_revert_knownCodePc(address(100), 0, 0, 0);
        }
        let messageData = bytearray_extract(calldata, 4, 32);
        messageData = bytearray_set256(messageData, 32, amount);
        queueMessageForSend(0, senderAddr, messageData);
        evmOp_return(0, 0);
    }

    evmOp_revert_knownCodePc(address(100), 0, 0, 0);
}

impure func arbsys_getTransactionCount(topFrame: EvmCallFrame, calldata: ByteArray) {
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(100), 0, 0, 0);
    }
    let addr = address(bytearray_get256(calldata, 4));
    let seqNum = account_getNextSeqNum(evmCallFrame_getAccount(topFrame, addr));
    let updatedMem = bytearray_set256(
         evmCallStack_getTopFrameMemoryOrDie(),
         0,
         seqNum
     );
    let success = evmCallStack_setTopFrameMemory(updatedMem);
    if (success) {
        evmOp_return(0, 32);
    } else {
        evmOp_revert_knownCodePc(address(100), 0, 0, 0);
    }
}

impure func arbsys_cloneContract(topFrame: EvmCallFrame, calldata: ByteArray) {
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(100), 0, 0, 0);
    }
    let oldContractAddr = address(bytearray_get256(calldata, 4));
    if let Some(newAddr) = evmCallStack_cloneContract(oldContractAddr) {
        let updatedMem = bytearray_set256(
            evmCallStack_getTopFrameMemoryOrDie(),
            0,
            uint(newAddr)
        );
        let success = evmCallStack_setTopFrameMemory(updatedMem);
        if (success) {
            evmOp_return(0, 32);
        } else {
            evmOp_revert_knownCodePc(address(100), 0, 0, 0);
        }
    } else {
        evmOp_revert_knownCodePc(address(100), 0, 0, 0);
    }
}

impure func arbsys_getStorageAt(topFrame: EvmCallFrame, calldata: ByteArray) {
    let senderAddr = evmCallFrame_getCaller(topFrame);
    if (senderAddr != address(0)) {
        evmOp_revert_knownCodePc(address(100), 0, 0, 0);
    }

    let addr = address(bytearray_get256(calldata, 4));
    let offset = bytearray_get256(calldata, 4+32);
    let acct = evmCallFrame_getAccount(topFrame, addr);

    let ret = 0;
    if let Some(val) = account_getStorageCell(acct, offset) {
        ret = val;
    }

    let updatedMem = bytearray_set256(evmCallStack_getTopFrameMemoryOrDie(), 0, ret);
    let success = evmCallStack_setTopFrameMemory(updatedMem);

    if (success) {
        evmOp_return(0, 32);
    } else {
        evmOp_revert_knownCodePc(address(100), 0, 0, 0);
    }
}
