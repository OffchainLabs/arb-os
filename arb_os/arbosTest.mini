//
// Copyright 2020, Offchain Labs, Inc. All rights reserved.
//

use std::bytearray::ByteArray;
use std::bytearray::bytearray_size;
use std::bytearray::bytearray_new;
use std::bytearray::bytearray_extract;
use std::bytearray::bytearray_get256;
use std::bytearray::bytearray_set256;
use std::bytearray::bytearray_copy;

use std::bytestream::bytestream_new;

use std::storageMap::storageMap_serialize;
use std::storageMap::storageMap_deserialize;

use accounts::accountStore_createOrAddCodeRef;
use accounts::accountStore_get;
use accounts::pristineAccount;
use accounts::account_setContractInfo;
use accounts::account_getAllStorage;
use accounts::account_getNextSeqNum;
use accounts::account_setNextSeqNum;
use accounts::account_getEthBalance;
use accounts::account_setEthBalance;
use accounts::account_getAllStorage;
use accounts::account_setStorage;

use codeSegment::translateEvmCodeSegment;

use evmOps::evmOp_return;
use evmOps::evmOp_revert_knownPc;
use evmOps::evmOp_revert_knownCodePc;

use evmCallStack::EvmCallFrame;
use evmCallStack::evmCallStack_callHitError;
use evmCallStack::evmCallStack_topFrame;
use evmCallStack::evmCallFrame_getCalldata;
use evmCallStack::evmCallStack_setTopFrameMemory;
use evmCallStack::evmCallStack_setAccount;
use evmCallStack::evmCallStack_getAccountStoreInCurrentContext;
use evmCallStack::evmCallStack_getAccountInCurrentContext;
use evmCallStack::evmCallStack_setAccountInCurrentContext;
use evmCallStack::evmCallFrame_getCaller;
use evmCallStack::evmCallFrame_getAccount;

use std::storageMap::storageMap_new;
use std::storageMap::storageMap_set;


public impure func arbosTest_txcall() {
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        if (bytearray_size(calldata) < 4) {
            evmOp_revert_knownCodePc(address(const::Address_ArbosTest), 1, 0, 0);
        }
        let funcCode = getFuncCode(calldata);
        if (funcCode == const::funcCode_ArbosTest_installAccount) {
            arbosTest_installAccount(topFrame, calldata);
        } elseif (funcCode == const::funcCode_ArbosTest_setNonce) {
            arbosTest_setNonce(topFrame, calldata);
        } elseif (funcCode == const::funcCode_ArbosTest_setBalance) {
            arbosTest_setBalance(topFrame, calldata);
        } elseif (funcCode == const::funcCode_ArbosTest_setStorage) {
            arbosTest_setStorage(topFrame, calldata);
        } elseif (funcCode == const::funcCode_ArbosTest_setCode) {
            arbosTest_setCode(topFrame, calldata);
        } elseif (funcCode == const::funcCode_ArbosTest_getMarshalledStorage) {
            arbosTest_getMarshalledStorage(topFrame, calldata);
        } elseif (funcCode == const::funcCode_ArbosTest_getAccountInfo) {
            arbosTest_getAccountInfo(topFrame, calldata);
        } elseif (funcCode == const::funcCode_ArbosTest_burnArbGas) {
            arbosTest_burnArbGas(topFrame, calldata);
        } else {
            // unrecognized function code
            evmOp_revert_knownCodePc(address(const::Address_ArbosTest), 2, 0, 0);
        }
    } else {
        // this shouldn't happen -- should always be called in an EVM tx
        evmCallStack_callHitError(19);
    }
}

func getFuncCode(ba: ByteArray) -> uint {
    return asm(224, bytearray_get256(ba, 0)) uint { shr };
}

impure func arbosTest_installAccount(topFrame: EvmCallFrame, calldata: ByteArray) { // (bool, uint, uint, bytes, bytes)
    // Verify that this has been called by address zero.
    // This ensures that this method can only be called (1) in testing, or (2) by the EthBridge.
    if (evmCallFrame_getCaller(topFrame) != address(0)) {
        evmOp_revert_knownCodePc(address(const::Address_ArbosTest), 10, 0, 0);
    }

    if (bytearray_size(calldata) <= 4+5*32) {
        evmOp_revert_knownCodePc(address(const::Address_ArbosTest), 11, 0, 0);
    }
    let addr = address(bytearray_get256(calldata, 4));
    let isEOA = bytearray_get256(calldata, 4+32) != 0;
    let balance = bytearray_get256(calldata, 4+2*32);
    let nonce = bytearray_get256(calldata, 4+3*32);

    let acct = pristineAccount(addr) with {
        ethBalance: balance
    } with {
        nextSeqNum: nonce
    };

    if( ! isEOA) {
        let codeSize = bytearray_get256(calldata, 4+6*32);
        let code = bytearray_extract(calldata, 4+7*32, codeSize);
        let storageOffset = 4 + 7*32 + 32*((codeSize+31)/32);
        let storageSize = bytearray_get256(calldata, storageOffset);
        let marshalledStorage = bytearray_extract(calldata, storageOffset+32, storageSize);

        if let Some(initStorage) = storageMap_deserialize(marshalledStorage) {
            let (startCodePoint, evmJumpTable, _) = translateEvmCodeSegment(bytestream_new(code), false);
            acct = account_setContractInfo(
                acct,
                code,
                evmJumpTable,
                startCodePoint,
                initStorage,
                false,
            );
        } else {
            evmOp_revert_knownCodePc(address(const::Address_ArbosTest), 12, 0, 0);
        }
    }

    if (evmCallStack_setAccount(addr, acct)) {
        evmOp_return(0, 0);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbosTest), 13, 0, 0);
    }
}

impure func arbosTest_setNonce(topFrame: EvmCallFrame, calldata: ByteArray) {
    // Verify that this has been called by address zero.
    // This ensures that this method can only be called (1) in testing, or (2) by the EthBridge.
    if (evmCallFrame_getCaller(topFrame) != address(0)) {
        evmOp_revert_knownPc(50, 0, 0);
    }
    if (bytearray_size(calldata) != 68) {
        evmOp_revert_knownPc(51, 0, 0);
    }
    let addr = address(bytearray_get256(calldata, 4));
    let newNonce = bytearray_get256(calldata, 36);
    let acct = evmCallStack_getAccountInCurrentContext(addr);
    evmCallStack_setAccountInCurrentContext(addr, account_setNextSeqNum(acct, newNonce));
    evmOp_return(0, 0);
}

impure func arbosTest_setBalance(topFrame: EvmCallFrame, calldata: ByteArray) {
    // Verify that this has been called by address zero.
    // This ensures that this method can only be called (1) in testing, or (2) by the EthBridge.
    if (evmCallFrame_getCaller(topFrame) != address(0)) {
        evmOp_revert_knownPc(60, 0, 0);
    }
    if (bytearray_size(calldata) != 68) {
        evmOp_revert_knownPc(61, 0, 0);
    }
    let addr = address(bytearray_get256(calldata, 4));
    let newBalance = bytearray_get256(calldata, 36);
    let acct = evmCallStack_getAccountInCurrentContext(addr);
    evmCallStack_setAccountInCurrentContext(addr, account_setEthBalance(acct, newBalance));
    evmOp_return(0, 0);
}

impure func arbosTest_setStorage(topFrame: EvmCallFrame, calldata: ByteArray) {
    // Verify that this has been called by address zero.
    // This ensures that this method can only be called (1) in testing, or (2) by the EthBridge.
    if (evmCallFrame_getCaller(topFrame) != address(0)) {
        evmOp_revert_knownPc(70, 0, 0);
    }

    let addr = address(bytearray_get256(calldata, 4));
    let isDiff = bytearray_get256(calldata, 36) != 0;
    let marshalledSize = bytearray_get256(calldata, 4+3*32);
    let marshalledStorage = bytearray_extract(calldata, 4+4*32, marshalledSize);

    let acct = evmCallStack_getAccountInCurrentContext(addr);
    let storage = xif (isDiff) {
        xif let Some(store) = account_getAllStorage(evmCallStack_getAccountInCurrentContext(addr)) {
            store
        } else {
            storageMap_new()
        }
    } else {
        storageMap_new()
    };
    let i = 0;
    while(64*i < marshalledSize) {
        let index = bytearray_get256(marshalledStorage, i*64);
        let value = bytearray_get256(marshalledStorage, i*64+32);
        storage = storageMap_set(storage, index, value);
        i = i + 1;
    }
    if let Some(uAcct) = account_setStorage(acct, storage) {
        evmCallStack_setAccountInCurrentContext(addr, uAcct);
        evmOp_return(0, 0);
    } else {
        evmOp_revert_knownPc(71, 0, 0);
    }
}

impure func arbosTest_setCode(topFrame: EvmCallFrame, calldata: ByteArray) {
    // Verify that this has been called by address zero.
    // This ensures that this method can only be called (1) in testing, or (2) by the EthBridge.
    if (evmCallFrame_getCaller(topFrame) != address(0)) {
        evmOp_revert_knownPc(80, 0, 0);
    }

    let addr = address(bytearray_get256(calldata, 4));
    let codeSize = bytearray_get256(calldata, 4+2*32);
    let code = bytearray_extract(calldata, 4+3*32, codeSize);

    let acctStore = evmCallStack_getAccountStoreInCurrentContext();
    if let Some(res) = accountStore_createOrAddCodeRef(acctStore, code) {
        let (uAcctStore, startCodePoint, evmJumpTable, _) = res;
        let acct = accountStore_get(acctStore, addr);
        let acct = account_setContractInfo(
            acct,
            code,
            evmJumpTable,
            startCodePoint,
            xif let Some(storage) = account_getAllStorage(acct) {
                storage
            } else {
                evmOp_revert_knownPc(81, 0, 0);
                //NOTREACHED
                storageMap_new()
            },
            false,
       );
       evmCallStack_setAccountInCurrentContext(addr, acct);
       evmOp_return(0, 0);
    } else {
        evmOp_revert_knownPc(82, 0, 0);
    }
}

impure func arbosTest_getMarshalledStorage(topFrame: EvmCallFrame, calldata: ByteArray) { // (address) -> bytes
    // Return the marshalled storage for the contract at addr. Revert if no contract at that address.
    if (evmCallFrame_getCaller(topFrame) != address(0)) {
        evmOp_revert_knownCodePc(address(const::Address_ArbosTest), 20, 0, 0);
    }

    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbosTest), 21, 0, 0);
    }

    let addr = address(bytearray_get256(calldata, 4));
    if let Some(storage) = account_getAllStorage(evmCallFrame_getAccount(topFrame, addr)) {
        let marshalledStorage = storageMap_serialize(storage);
        let msSize = bytearray_size(marshalledStorage);
        let mem = bytearray_set256(
            bytearray_set256(bytearray_new(0), 0, 64),
            32,
            msSize
        );
        mem = bytearray_copy(marshalledStorage, 0, mem, 64, msSize);
        if (evmCallStack_setTopFrameMemory(mem)) {
            evmOp_return(0, 64+msSize);
        } else {
            evmOp_revert_knownCodePc(address(const::Address_ArbosTest), 22, 0, 0);
        }
    } else {
        evmOp_return(0, 0);  // tests expect a report of empty storage if there is no storage
    }
}

impure func arbosTest_getAccountInfo(topFrame: EvmCallFrame, calldata: ByteArray) { // (address) -> bytes
    // Return the balance, nonce, and marshalled storage for the contract at addr. Revert if no contract at that address.
    if (evmCallFrame_getCaller(topFrame) != address(0)) {
        evmOp_revert_knownCodePc(address(const::Address_ArbosTest), 20, 0, 0);
    }

    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbosTest), 21, 0, 0);
    }

    let addr = address(bytearray_get256(calldata, 4));
    let acct = evmCallFrame_getAccount(topFrame, addr);
    let mem = bytearray_set256(
        bytearray_set256(bytearray_new(0), 0, account_getEthBalance(acct)),
        32,
        account_getNextSeqNum(acct),
    );
    if let Some(storage) = account_getAllStorage(acct) {
        let marshalledStorage = storageMap_serialize(storage);
        let msSize = bytearray_size(marshalledStorage);
        mem = bytearray_copy(marshalledStorage, 0, mem, 64, msSize);
    }
    if (evmCallStack_setTopFrameMemory(mem)) {
        evmOp_return(0, bytearray_size(mem));
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbosTest), 22, 0, 0);
    }
}

impure func arbosTest_burnArbGas(_topFrame: EvmCallFrame, calldata: ByteArray) { // (address) -> bytes
    // Burn a specified amount of ArbGas
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbosTest), 30, 0, 0);
    }
    let gasToBurn = bytearray_get256(calldata, 4) * const::Charging_DefaultArbGasDivisor;
    let gasLeft = asm() uint { getgas };
    if (gasToBurn >= gasLeft) {
        gasToBurn = gasLeft - 1;
    }
    asm(gasLeft - gasToBurn,) { setgas };
    evmOp_return(0, 0);
}
