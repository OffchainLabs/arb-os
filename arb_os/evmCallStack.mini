//
// Copyright 2020-2021, Offchain Labs, Inc. All rights reserved.
//

use accounts::AccountStore;
use accounts::Account;
use evmlogs::EvmLogs;
use std::queue::Queue;
use std::bytearray::ByteArray;
use std::stack::Stack;
use messages::TxRequestData;

use errorHandler::errorHandler;

use accounts::getGlobalAccountStore;
use accounts::setGlobalAccountStore;
use accounts::accountStore_changeNumContracts;
use accounts::accountStore_get;
use accounts::accountStore_set;
use accounts::accountStore_getRetryBuffer;
use accounts::accountStore_setRetryBuffer;
use accounts::accountStore_transferEthBalance;
use accounts::accountStore_destroyAccount;
use accounts::accountStore_createOrAddCodeRef;
use accounts::accountStore_payFromEscrow;
use accounts::account_setStorageCell;
use accounts::account_getBalance;
use accounts::account_getAllStorage;
use accounts::account_getStartCodePoint;
use accounts::account_setContractInfo;
use accounts::account_incrSeqNum;
use accounts::account_getStorageSize;

use chainParameters::chainParams_getOrDie;
use codeSegment::getDummyAcceptContractCode;

use evmOps::evmOp_callAndReturn;

use evmlogs::evmlogs_empty;
use evmlogs::evmlogs_appendAny;

use std::avmcodebuilder::AvmCodePoint;

use std::bytearray::bytearray_new;
use std::bytearray::bytearray_size;
use std::bytearray::bytearray_extract;
use std::bytearray::bytearray_copy;
use std::bytearray::bytearray_set256;
use std::bytestream::bytestream_new;

use std::storageMap::storageMap_new;

use std::queue::queue_new;
use std::queue::queue_isEmpty;
use std::queue::queue_get;
use std::queue::queue_put;

use std::safeMath::safeMul;

use std::stack::stack_new;
use std::stack::stack_isEmpty;
use std::stack::stack_push;
use std::stack::stack_pop;
use std::stack::stack_discardDeepestItems;

use gasAccounting::GasUsage;
use gasAccounting::gasAccounting_startTxCharges;
use gasAccounting::gasAccounting_endTxCharges;
use gasAccounting::gasAccounting_pauseTxCharges;
use gasAccounting::gasAccounting_resumeTxCharges;
use gasAccounting::txFeeStats_zero;
use gasAccounting::getAddressForReserveFundsDeposit;
use gasAccounting::deferredPayments_apply;
use gasAccounting::gasAccounting_addToGasPool;

use codeSegment::translateEvmCodeSegment;

use output::emitTxReceipt;
use output::queueMessageForSend;

use main::mainRunLoop;

use output::outbox_append;
use output::sendQueuedMessages;

use inbox::IncomingRequest;
use inbox::inbox_currentArbBlockNumber;
use inbox::inbox_currentEthBlockNumber;
use inbox::inbox_currentTimestamp;

use retrybuffer::RetryBuffer;
use retrybuffer::RetryableTx;
use retrybuffer::retryableTx_getOriginalSender;

use tracing::evmTracer_clear;
use tracing::evmTracer_pushCall;
use tracing::evmTracer_pushReturnRevert;
use tracing::evmTracer_pushCallAndResultNoExecution;


// This callframe is used to represent an EVM call in progress.
// The callframe will have a full copy of the global AccountStore, but
//        whether to ultimately adopt this AccountStore version will be contingent on the call succeeding.
// If the call succeeds, we'll copy the call's state into the caller as the new
//        state, but if the call fails we'll discard the state from the callframe.
// (For staticcalls, we will discard state even if the call succeeds, as the EVM spec requires.)
type EvmCallFrame = struct {
    runningAs: address,                 // call is running in the execution/security context of this address
    runningCodeFrom: address,           // call is running code associated with this address
    accountStore: AccountStore,         // accountStore used for executing this call
    runningAsAccount: Account,          // cache of accountStore[runningAs]
    caller: address,                    // who called this
    isConstructor: bool,                // true iff this is a constructor
    static: bool,                       // static flag, per EIP-214
    calldata: ByteArray,                // calldata passed in by our caller
    callvalue: uint,                    // Eth value given to this call by its caller
    returnInfo: option<ReturnInfo>,     // result of last subcall made by this call (if any)
    memory: ByteArray,                  // EVM memory of this call
    maxMemoryOffsetTouched: uint,       // maximum memory offset read/written by this call (in bytes)
    storageDelta: int,                  // change in total EVM storage usage, including child calls
    revertOnStorageWrite: bool,         // if true, revert if the EVM code tries to write to storage
    evmLogs: EvmLogs,                   // EVM logs, including those created by this call and its subcalls
    selfDestructQueue: Queue,           // set of self-destruct directives, including those created by this call and its subcalls
    resumeInfo: option<ResumeInfo>,     // if call is suspended, its state will be saved here
    sendQueue: Queue,                   // outgoing messages, including those created by this call and its subcalls
    sendOnFailure: option<ByteArray>,   // message to send if call fails
    parent: option<EvmCallFrame>,       // call-frame that created this one, or None if this is a top-level call
}

type ReturnInfo = struct {  // information about a call that previously completed
    data: ByteArray,
    status: bool,
}

type ResumeInfo = struct {            // information used to resume a call after a subcall returns
    codePoint: func(),                // where to resume execution
    stackContents: Stack,             // saved contents of stack
    auxStackContents: option<Stack>,  // saved contents of aux stack; None means to clear the auxstack on resume
    returnDataOffset: uint,           // where returndata from subcall should be put
    returnDataNbytes: uint,           // max amount of returndata to copy into caller's memory
    callType: uint,                   // call type of the subcall
    savedGas: uint,                   // how much of the caller's gas was not lent to the subcall
    givenGas: uint,                   // how much of the call's gas was lent to the subcall
}

var globalCallStack: option<EvmCallFrame>;   // call frame of currently running call, if any
var globalCurrentTxRequest: TxRequestData;  // request that launched the top-level call

type IncomingRequestAndLogs = struct {
    req: IncomingRequest,
    evmLogs: EvmLogs,
    originalSender: address,
}

var globalCurrentRetryableRequest: option<IncomingRequestAndLogs>;  // describes the retryable request we're currently doing, if any

// This is designed to be called if a top-level, non-constructor tx succeeds. First arg is tx's returndata.
type TxReturndataHook = struct {
    callback: impure func(ByteArray, any),
    data: any,
}

public impure func _evmCallStack_currentArbBlockNumber() -> uint {
    return globalCurrentTxRequest.incomingRequest.arbBlockNumber;
}

public impure func evmCallStack_currentEthBlockNumber() -> uint {
    return globalCurrentTxRequest.incomingRequest.ethBlockNumber;
}

public impure func evmCallStack_currentTimestamp() -> uint {
    return globalCurrentTxRequest.incomingRequest.timestamp;
}

public impure func evmCallStack_init() {
    globalCallStack = None<EvmCallFrame>;
    globalCurrentRetryableRequest = None<IncomingRequestAndLogs>;
}

public impure func getRetryBufferInCurrentContext() -> RetryBuffer {
    let astore = xif let Some(frame) = globalCallStack {
        frame.accountStore
    } else {
        getGlobalAccountStore()
    };
    return accountStore_getRetryBuffer(astore);
}

public impure func setRetryBufferInCurrentContext(rbuf: RetryBuffer) {
    if let Some(frame) = globalCallStack {
        globalCallStack = Some(frame with {
            accountStore: accountStore_setRetryBuffer(frame.accountStore, rbuf)
        });
    } else {
        setGlobalAccountStore(
            accountStore_setRetryBuffer(getGlobalAccountStore(), rbuf)
        );
    }
}

public impure func evmCallStack_isEmpty() -> bool {                                                 
    return globalCallStack == None<EvmCallFrame>;                                                   
}

public impure func evmCallStack_currentTxRequestGasBid() -> uint {
    return globalCurrentTxRequest.gasPrice;
}

public impure func evmCallStack_addStorageCharge(units: uint) {
    if let Some(topFrame) = globalCallStack {
        globalCallStack = Some(
            topFrame with {
                storageDelta: topFrame.storageDelta + int(units)
            }
        );
    }
}

public impure func evmCallStack_stackDepth() -> uint {
    let depth = 0;
    let maybeFrame = globalCallStack;
    return xloop {
        if let Some(frame) = maybeFrame {
            depth = depth + 1;
            maybeFrame = frame.parent;
        } else {
            return depth;
        }
    };
}

public impure func evmCallStack_inRetryable() -> bool {
    return (globalCallStack != None<EvmCallFrame>) && (globalCurrentRetryableRequest != None<IncomingRequestAndLogs>);
}

public impure func evmCallStack_inStaticCall() -> bool {
    return xif let Some(topFrame) = globalCallStack {
        topFrame.static
    } else {
        false
    };
}

public impure func evmCallStack_stackInfo() -> (uint, (address, address), (address, address)) {
    return xif let Some(topFrame) = globalCallStack {
        let sndRunningAs = address(0);
        let sndRunningCodeFrom = address(0);
        if let Some(sndFrame) = unsafecast<option<EvmCallFrame>>(topFrame.parent) {
            sndRunningAs = sndFrame.runningAs;
            sndRunningCodeFrom = sndFrame.runningCodeFrom;
        }
        (
            globalCurrentTxRequest.incomingRequest.requestId,
            (topFrame.runningAs, topFrame.runningCodeFrom),
            (sndRunningAs, sndRunningCodeFrom),
        )
    } else {
        (0, (address(0), address(0)), (address(0), address(0)))
    };
}

public impure func initEvmCallStack(
    callKind: uint,
    request: TxRequestData,
    sendOnFailure: option<ByteArray>,
    callvalueEscrowKey: option<uint>,
) {
    evmTracer_clear();
    evmTracer_pushCall(
        callKind,
        request.calldata,
        request.value,
        request.caller,
        request.calleeAddr,
        request.maxGas,
        request.gasPrice,
    );

    if let Some(entryPoint) = account_getStartCodePoint(
        accountStore_get(
            getGlobalAccountStore(),
            request.calleeAddr
        )
    ) {
        globalCurrentTxRequest = request;
        globalCurrentRetryableRequest = None<IncomingRequestAndLogs>;

        let (startTxResult, acctStore) = xif(request.maxGas < const::Charging_MinArbGasForContractTx) {
            (const::TxResultCode_belowMinimumTxGas, getGlobalAccountStore())
        } else {
            let (res, astore) = gasAccounting_startTxCharges(
            	request.maxGas, 
            	request.gasPrice, 
            	request.gasPayer, 
            	getGlobalAccountStore(), 
            );
            setGlobalAccountStore(astore);
            (res, astore)
        };

        if (startTxResult == const::TxResultCode_success) {
            if (request.seqNum != None<uint>) {
                // increment the caller's sequence number
                acctStore = accountStore_set(
                    acctStore,
                    request.caller,
                    account_incrSeqNum(
                        accountStore_get(acctStore, request.caller)
                    )
                );
            }
            setGlobalAccountStore(acctStore);

            globalCurrentTxRequest = request;
            globalCurrentRetryableRequest = None<IncomingRequestAndLogs>;

            // pay callvalue from caller to the called contract
            // payment will only be recorded inside the callframe, so it will be discarded if the call reverts
            if let Some(globalAS) = ethTransferOrGrant(
                getGlobalAccountStore(),
                request.caller,
                request.calleeAddr,
                request.value,
                callvalueEscrowKey,
            ) {
                globalCallStack = Some(struct {
                    runningAs: request.calleeAddr,
                    runningCodeFrom: request.calleeAddr,
                    accountStore: globalAS,
                    runningAsAccount: accountStore_get(globalAS, request.calleeAddr),
                    caller: request.caller,
                    isConstructor: false,
                    static: false,
                    calldata: request.calldata,
                    callvalue: request.value,
                    returnInfo: None<ReturnInfo>,
                    memory: bytearray_new(0),
                    maxMemoryOffsetTouched: 0,
                    storageDelta: int(0),
                    revertOnStorageWrite: false,
                    evmLogs: evmlogs_empty(),
                    selfDestructQueue: queue_new(),
                    resumeInfo: None<ResumeInfo>,
                    sendQueue: queue_new(),
                    sendOnFailure: sendOnFailure,
                    parent: None<EvmCallFrame>
                });

                emptyAvmStack();
                asm(entryPoint,) { jump };
                // entryPoint();  // start executing the call; should never return
            } else {
                if let Some(res) = gasAccounting_endTxCharges(int(0), request.feeStats) {
                    // insufficient balance error
                    let (_, gasUsage, feeStats, payments) = res;
                    if let Some(uas) = deferredPayments_apply(getGlobalAccountStore(), payments) {
                        setGlobalAccountStore(uas);
                    }
                    evmTracer_pushReturnRevert(const::TxResultCode_insufficientBalance, bytearray_new(0), 0);
                    emitTxReceipt(
                        request.incomingRequest,
                        const::TxResultCode_insufficientBalance,
                        None<ByteArray>,
                        None<EvmLogs>,
                        Some(gasUsage),
                        feeStats,
                        true,
                        request.originalCaller,
                    );
                } else {
                    // unknown error type
                    evmTracer_pushReturnRevert(const::TxResultCode_unknownFailure, bytearray_new(0), 0);
                    emitTxReceipt(
                        request.incomingRequest,
                        const::TxResultCode_unknownFailure,
                        None<ByteArray>,
                        None<EvmLogs>,
                        None<GasUsage>,
                        request.feeStats,
                        true,
                        request.originalCaller,
                    );
                }
                evmCallStack_callHitError(1);
            }
        } else {
            // can't pay for gas error
            evmTracer_pushReturnRevert(startTxResult, bytearray_new(0), 0);
            emitTxReceipt(
                request.incomingRequest,
                startTxResult,
                None<ByteArray>,
                None<EvmLogs>,
                None<GasUsage>,
                request.feeStats,
                true,
                request.originalCaller,
            );
            evmCallStack_callHitError(2);
        }
    } else {
        // no contract at this address, so treat tx as a simple eth transfer

        if (request.seqNum != None<uint>) {
            // increment the caller's sequence number
            let acctStore = getGlobalAccountStore();
            acctStore = accountStore_set(
                acctStore,
                request.caller,
                account_incrSeqNum(
                    accountStore_get(acctStore, request.caller)
                )
            );
            setGlobalAccountStore(acctStore);
        }

        if (request.value > 0) {
            // transfer value to destination address
            if let Some(globalAS) = ethTransferOrGrant(
                getGlobalAccountStore(),
                request.caller,
                request.calleeAddr,
                request.value,
                callvalueEscrowKey,
            ) {
                if (callKind != const::EVMCallType_staticcall) {  // commit the transfer, unless it's a static call
                    setGlobalAccountStore(globalAS);
                }

                // report success
                evmTracer_pushReturnRevert(const::TxResultCode_success, bytearray_new(0), 0);
                emitTxReceipt(
                    request.incomingRequest,
                    const::TxResultCode_success,
                    None<ByteArray>,
                    None<EvmLogs>,
                    None<GasUsage>,
                    request.feeStats,
                    true,
                    request.originalCaller,
                );
                emptyAvmStack();
                cleanAvmAuxStackAndCall(mainRunLoop, true);  // should never return
            } else {
                // insufficient balance error
                evmTracer_pushReturnRevert(const::TxResultCode_insufficientBalance, bytearray_new(0), 0);
                emitTxReceipt(
                    request.incomingRequest,
                    const::TxResultCode_insufficientBalance,
                    None<ByteArray>,
                    None<EvmLogs>,
                    None<GasUsage>,
                    request.feeStats,
                    true,
                    request.originalCaller,
                );
                evmCallStack_callHitError(3);
            }
        } else {
            // tx is transfer of 0 Eth, so just report success
            evmTracer_pushReturnRevert(const::TxResultCode_success, bytearray_new(0), 0);
            emitTxReceipt(
                request.incomingRequest,
                const::TxResultCode_success,
                None<ByteArray>,
                None<EvmLogs>,
                None<GasUsage>,
                request.feeStats,
                true,
                request.originalCaller,
            );
            emptyAvmStack();
            cleanAvmAuxStackAndCall(mainRunLoop, true);  // should never return
        }
    }
}

public impure func initEvmCallStackForConstructor(
    code: ByteArray,
    request: TxRequestData,
    callvalueEscrowKey: option<uint>,
) {
    // Set up the call frame for a top-level constructor call, and launch the call's execution.
    // This is called in the context of a new account.
    // The constructor will run, and (if it succeeds) it will return code, which will become the new
    //      code for this new account.

    evmTracer_clear();
    evmTracer_pushCall(
        const::EVMCallType_call,
        request.calldata,
        request.value,
        request.caller,
        request.calleeAddr,
        request.maxGas,
        request.gasPrice,
    );

    globalCurrentTxRequest = request with { isConstructor: true };
    globalCurrentRetryableRequest = None<IncomingRequestAndLogs>;

    // Create an accountStore with the caller's sequence number incremented, and commit it
    let acctStore = getGlobalAccountStore();
    acctStore = accountStore_set(
        acctStore,
        request.caller,
        account_incrSeqNum(
            accountStore_get(acctStore, request.caller)
        )
    );
    setGlobalAccountStore(acctStore);

    if let Some(uas) = ethTransferOrGrant(
        acctStore,
        request.caller,
        request.calleeAddr,
        request.value,
        callvalueEscrowKey,
    ) {
        acctStore = uas;

        let (startTxResult, uAcctStore) = xif(request.maxGas < const::Charging_MinArbGasForContractTx) {
            (const::TxResultCode_belowMinimumTxGas, acctStore)
        } else {
            gasAccounting_startTxCharges(
                request.maxGas, 
                request.gasPrice, 
                request.gasPayer, 
                acctStore,
            )
        };
        acctStore = uAcctStore;

        if (startTxResult == const::TxResultCode_success) {
            setGlobalAccountStore(acctStore);   // commit the incremented sequence number

            // set up a fake stack frame, so errors in translateEvmCodeSegment cause failure receipts
            globalCallStack = Some(struct {
                runningAs: request.calleeAddr,
                runningCodeFrom: request.calleeAddr,
                accountStore: acctStore,
                runningAsAccount: accountStore_get(acctStore, request.calleeAddr),
                caller: request.caller,
                isConstructor: true,
                static: false,
                calldata: request.calldata,
                callvalue: request.value,
                returnInfo: None<ReturnInfo>,
                memory: bytearray_new(0),
                maxMemoryOffsetTouched: 0,
                storageDelta: int(0),
                revertOnStorageWrite: false,
                evmLogs: evmlogs_empty(),
                selfDestructQueue: queue_new(),
                resumeInfo: None<ResumeInfo>,
                sendQueue: queue_new(),
                sendOnFailure: None<ByteArray>,
                parent: None<EvmCallFrame>
            });

            let (startPoint, evmJumpTable, _) = translateEvmCodeSegment(bytestream_new(code), false);

            // Create a new account to run the constructor code.
            acctStore = accountStore_set(
                acctStore,
                request.calleeAddr,
                account_setContractInfo(
                    accountStore_get(acctStore, request.calleeAddr),
                    code,
                    evmJumpTable,
                    getDummyAcceptContractCode(),
                    storageMap_new(),
                    true,
                )
            );

            globalCallStack = Some(struct {
                runningAs: request.calleeAddr,
                runningCodeFrom: request.calleeAddr,
                accountStore: acctStore,
                runningAsAccount: accountStore_get(acctStore, request.calleeAddr),
                caller: request.caller,
                isConstructor: true,
                static: false,
                calldata: request.calldata,
                callvalue: request.value,
                returnInfo: None<ReturnInfo>,
                memory: bytearray_new(0),
                maxMemoryOffsetTouched: 0,
                storageDelta: int(0),
                revertOnStorageWrite: false,
                evmLogs: evmlogs_empty(),
                selfDestructQueue: queue_new(),
                resumeInfo: None<ResumeInfo>,
                sendQueue: queue_new(),
                sendOnFailure: None<ByteArray>,
                parent: None<EvmCallFrame>
            });

            emptyAvmStack();
            asm(startPoint,) { jump };
            // startPoint();  // start executing the call; should never return
            panic;
        } else {
            // can't pay for gas error
            evmTracer_pushReturnRevert(startTxResult, bytearray_new(0), 0);
            emitTxReceipt(
                request.incomingRequest,
                startTxResult,
                None<ByteArray>,
                None<EvmLogs>,
                None<GasUsage>,
                request.feeStats,
                true,
                request.originalCaller,
            );
            evmCallStack_callHitError(15);
        }
    } else {
        // caller doesn't have the funds to provide callvalue
        evmTracer_pushReturnRevert(const::TxResultCode_insufficientBalance, bytearray_new(0), 0);
        emitTxReceipt(
            request.incomingRequest,
            const::TxResultCode_insufficientBalance,
            None<ByteArray>,
            None<EvmLogs>,
            None<GasUsage>,
            request.feeStats,
            true,
            request.originalCaller,
        );
        evmCallStack_callHitError(4);  // should never return
        panic;
    }
}

public impure func evmCallStack_doCall(
    kind: uint,  // currently handle 0 (call), 1 (callcode), 2 (delegatecall), 3 (staticcall)
    isConstructor: bool,
    gas: uint,
    calleeAddr: address,
    balance: uint,
    argsOffset: uint,
    argsLength: uint,
    retOffset: uint,
    retLength: uint,
    resumeCodePoint: func(),          // jump to this to resume the caller
    resumeAuxStack: option<Stack>,    // aux stack contents to restore on resume, if any
    callvalueEscrowKey: option<uint>, // escrow key that supplies the callvalue (otherwise caller supplies it)
    overrideStartPoint: option<AvmCodePoint>,
) -> option<bool> {   // returns Some(success) if succeed/fail, or None on error
    if (   (kind != const::EVMCallType_call)
        && (kind != const::EVMCallType_staticcall)
        && (uint(calleeAddr) >= const::MinArbitrumPrecompile)
        && (uint(calleeAddr) <= const::MaxArbitrumPrecompile) ) {
        return Some(false);
    }
    let topFrame = globalCallStack?;
    let callee = accountStore_get(topFrame.accountStore, calleeAddr);
    let maybeStartPoint = xif let Some(osp) = overrideStartPoint {
        overrideStartPoint
    } else {
        account_getStartCodePoint(callee)
    };
    if let Some(startCodePoint) = maybeStartPoint {
        // Create a new callframe for a subcall, and transfer execution to it.
        let availableGas = gasAccounting_pauseTxCharges();
        if (balance > 0) {
            // Add a stipend, charged to the caller, if non-zero value is transferred.
            // This is needed because Ethereum provides a stipend, and some code depends on that.
            // The size of our stipend was chosen experimentally, to make sure a call that will run on Ethereum will run here.
            gas = gas + chainParams_getOrDie(const::Atom_Param_ArbitrumNonZeroBalanceCallStipend);
        }
        if (gas > 63*availableGas/64) {
            gas = 63*availableGas/64;  // don't give the callee more than 63/64 of the call's gas
        }

        let callerAddr = topFrame.runningAs;
        let caller = topFrame.runningAsAccount;

        // prepare the caller's frame
        //    this means (1) saving the runningAsAccount into the accountStore, and
        //               (2) saving the resumeInfo so we can resume later
        let stackContents = saveAvmStack();  // must be done as a top level statement
        topFrame = topFrame
            with { accountStore: accountStore_set(topFrame.accountStore, callerAddr, caller) }
            with { resumeInfo: Some(struct {
                       codePoint: resumeCodePoint,
                       stackContents: stackContents,
                       auxStackContents: resumeAuxStack,
                       returnDataOffset: retOffset,
                       returnDataNbytes: retLength,
                       callType: kind,
                       savedGas: availableGas-gas,
                       givenGas: gas,
                   }) };

        // now create a new callframe for the callee
        //     it incorporates the balance transfer from caller to callee
        //          (which is put into the callee frame so it isn't committed unless the call succeeds)
        let runningAsAddr = calleeAddr;
        let newCallerAddr = topFrame.runningAs;
        if ( (kind == const::EVMCallType_callcode) || (kind == const::EVMCallType_delegatecall) ) {
            runningAsAddr = callerAddr;  // callcode and delegatecall run as the caller, with callee's code
            if (kind == const::EVMCallType_delegatecall) {
                newCallerAddr = topFrame.caller;  // delegatecall inherits the caller of its parent
            }
        }
        if (   (calleeAddr != runningAsAddr)
            && (uint(calleeAddr) >= const::MinArbitrumPrecompile)
            && (uint(calleeAddr) <= const::MaxArbitrumPrecompile)
        ) {
            return Some(false);
        }
        let updatedAcctStore = topFrame.accountStore;
        if (balance > 0) {
            if let Some(uas) = ethTransferOrGrant(
                updatedAcctStore,
                callerAddr,
                calleeAddr,
                balance,
                callvalueEscrowKey,
            ) {
                updatedAcctStore = uas;
            } else {
                restoreAvmStack(stackContents);
                return Some(false);
            }
        } elseif (kind == const::EVMCallType_delegatecall) {
            // delegatecall inherits the callvalue of its parent
            // but don't do a transfer because it's running as the same account
            balance = topFrame.callvalue;
        }
        let newTopFrame = struct {
            runningAs: runningAsAddr,
            runningCodeFrom: calleeAddr,
            accountStore: updatedAcctStore,
            runningAsAccount: accountStore_get(updatedAcctStore, runningAsAddr),
            caller: newCallerAddr,
            isConstructor: isConstructor,
            static: topFrame.static || (kind == const::EVMCallType_staticcall),
            calldata: bytearray_extract(topFrame.memory, argsOffset, argsLength),
            callvalue: balance,
            returnInfo: None<ReturnInfo>,
            memory: bytearray_new(0),
            maxMemoryOffsetTouched: 0,
            storageDelta: topFrame.storageDelta,
            revertOnStorageWrite: // disallow storage write if gas provided < EVM gas cost of storage write
                                  //       if balance == 0, disallow if gas < EVM gas cost of storage write
                                  //       if balance > 0, disallow if gas (before 20000 stipend) + EVM stipend
                                  //                           would have been less than EVM gas cost of storage write
                                  topFrame.revertOnStorageWrite
                                  || ((balance == 0) && (gas < const::EVMWriteL1GasCost))
                                  || ((balance > 0) && (gas <= (const::EVMWriteL1GasCost
                                                                + chainParams_getOrDie(const::Atom_Param_ArbitrumNonZeroBalanceCallStipend)
                                                                - const::EVMNonZeroBalanceCallStipend))),
            evmLogs: topFrame.evmLogs,
            selfDestructQueue: topFrame.selfDestructQueue,
            resumeInfo: None<ResumeInfo>,
            sendQueue: topFrame.sendQueue,
            sendOnFailure: None<ByteArray>,
            parent: Some(topFrame)
        };

        // there were no errors in the setup, so write the result to global storage
        globalCallStack = Some(newTopFrame);

        // write trace info
        evmTracer_pushCall(
            kind,
            newTopFrame.calldata,
            newTopFrame.callvalue,
            newTopFrame.caller,
            newTopFrame.runningCodeFrom,
            gas,
            globalCurrentTxRequest.gasPrice,
        );

        // jump to the called code's startCodePoint -- this should never return
        let _ = gasAccounting_resumeTxCharges(gas);
        emptyAvmStack();
        asm(startCodePoint,) { jump };
    } else {
        // no code at that address, so just do eth transfer
        let success = xif (balance > 0) {
            xif let Some(escrowKey) = callvalueEscrowKey {
                evmCallStack_transferEthFromEscrow(escrowKey, calleeAddr, balance) == Some(())
            } else {
                evmCallStack_transferEthFromCurrent(calleeAddr, balance) == Some(())
            }
        } else {
            true
        };

        // save returndata and status
        globalCallStack = Some(globalCallStack? with {
            returnInfo: Some(struct {
                data: bytearray_new(0),
                status: success,
            })
        });

        evmTracer_pushCallAndResultNoExecution(
            kind,
            bytearray_extract(topFrame.memory, argsOffset, argsLength),
            balance,
            topFrame.runningAs,
            calleeAddr,
            gas,
            globalCurrentTxRequest.gasPrice,
            xif (success) { const::TxResultCode_success } else { const::TxResultCode_revert },
        );
        return Some(success);
    }

    evmCallStack_callHitError(7);
    return panic;
}

public impure func evmCallStack_returnFromCall(
    success: bool,
    returnOffset: uint,
    returnLength: uint,
    jumpTargetIfTxEnd: option<impure func()>,
) -> option<()> {
    // A call just completed. Commit its effects as appropriate, and return control to its caller.

    let leftoverGas = gasAccounting_pauseTxCharges();  // amount of gas lent to call that wasn't used

    let topFrame = globalCallStack?;
    let memory = topFrame.memory;
    let returnData = bytearray_extract(memory, returnOffset, returnLength);

    return xif let Some(newTopFrame) = topFrame.parent {
        // Returning from a non-top-level call

        topFrame = topFrame with {   // copy cached account back into callframe's main accountStore
            accountStore: accountStore_set(
                topFrame.accountStore,
                topFrame.runningAs,
                topFrame.runningAsAccount
            )
        };

        if (success && !topFrame.static) {
            // copy relevant data back to parent frame
            newTopFrame = newTopFrame
                with { accountStore: topFrame.accountStore }
                with { runningAsAccount: accountStore_get(topFrame.accountStore, newTopFrame.runningAs) }
                with { storageDelta: topFrame.storageDelta }
                with { evmLogs: topFrame.evmLogs }
                with { sendQueue: topFrame.sendQueue }
                with { selfDestructQueue: topFrame.selfDestructQueue };
        }

        // set up returnInfo in caller's frame
        newTopFrame = newTopFrame with { returnInfo: Some(struct{ data: returnData, status: success }) };

        // copy returndata into caller's memory
        let resumeInfo = newTopFrame.resumeInfo?;
        if (returnLength > resumeInfo.returnDataNbytes) {
             returnLength = resumeInfo.returnDataNbytes;
        }
        let newMemory = bytearray_copy(
            returnData,
            0,
            newTopFrame.memory,
            resumeInfo.returnDataOffset,
            returnLength,
        );
        newTopFrame = newTopFrame with { memory: newMemory };

        evmTracer_pushReturnRevert(
            xif (success) { const::TxResultCode_success } else { const::TxResultCode_revert },
            returnData,
            resumeInfo.givenGas + resumeInfo.savedGas - leftoverGas,
        );

        // ready to return to caller
        // save updated EVM callstack state; send sendOnFailure message if applicable; restore caller's stack; jump to resume address
        globalCallStack = Some(newTopFrame);
        if ( ! success) {
            if let Some(msg) = topFrame.sendOnFailure {
                // promote the send-on-failure message to the next level of the callstack
                // note that it will still be discarded if the parent (or grandparent etc) reverts
                queueMessageForSend(msg);
            }
        }
        emptyAvmStack();                             // throw away anything that callee left on the stack
        restoreAvmStack(resumeInfo.stackContents,);  // restore the caller's stack
        asm(success,) { };                           // push return code onto the stack, where caller expects it to be
        let _ = gasAccounting_resumeTxCharges(leftoverGas + resumeInfo.savedGas);
        restoreAuxStackAndCall(resumeInfo.codePoint, resumeInfo.auxStackContents, false);

        evmCallStack_callHitError(8);
        panic  // shouldn't happen -- previous instruction jumped away
    } else {
        // top-level call completed; will need to return to external caller

        if (success) {
            let (entryPoint, evmJumpTable) = xif (globalCurrentTxRequest.isConstructor) {
                let _ = gasAccounting_resumeTxCharges(leftoverGas);
                let (uAcctStore, ep, ejt, created) = accountStore_createOrAddCodeRef(topFrame.accountStore, returnData)?;
                globalCallStack = Some(topFrame with { accountStore: uAcctStore });
                if (created) {
                    evmCallStack_addStorageCharge( (bytearray_size(returnData) + 31) / 32 );
                }
                topFrame = globalCallStack?;
                (ep, ejt)
            } else {
                (errorHandler, newmap<uint, func()>)
            };
            let (completed, gasUsage, feeStats, deferredPayments) = gasAccounting_endTxCharges(
                topFrame.storageDelta - int(storageReclaimedForSDQ(topFrame)),  // won't underflow because signed subtraction
                globalCurrentTxRequest.feeStats
            )?;

            if (completed) {
                if (globalCurrentTxRequest.isConstructor) {
                    // Constructor call was successful, so finish setting up the account.

                    // Use the storage left over by the constructor.
                    let storageMap = storageMap_new();
                    if let Some(sm) = account_getAllStorage(topFrame.runningAsAccount) {
                        storageMap = sm;
                    } else {
                        // Somehow the constructor didn't have storage; report error and recover.
                        evmTracer_pushReturnRevert(const::TxResultCode_formatError, bytearray_new(0), gasUsage.gasUsed);
                        emitTxReceipt(
                            globalCurrentTxRequest.incomingRequest,
                            const::TxResultCode_formatError,
                            None<ByteArray>,
                            None<EvmLogs>,
                            Some(gasUsage),
                            feeStats,
                            true,
                            globalCurrentTxRequest.originalCaller,
                        );
                        evmCallStack_callHitError(5);  // should never return
                        panic;
                    }

                    // Update the global account store
                    let acctStore = accountStore_set(
                        topFrame.accountStore,
                        topFrame.runningAs,
                        account_setContractInfo(
                            topFrame.runningAsAccount,
                            returnData,
                            evmJumpTable,
                            entryPoint,
                            storageMap,
                            true,
                        )
                    );
                    if let Some(uas) = deferredPayments_apply(acctStore, deferredPayments) {
                        acctStore = uas;
                    }
                    // Commit the self-destruct events that were requested during the call.
                    let sdq = topFrame.selfDestructQueue;
                    while( ! queue_isEmpty(sdq) ) {
                        let (q, item) = queue_get(sdq)?;
                        sdq = q;
                        let victim = unsafecast<address>(item);
                        acctStore = accountStore_changeNumContracts(
                            accountStore_destroyAccount(acctStore, victim),
                            -1s,
                        );
                    }
                    setGlobalAccountStore(acctStore);

                    returnData = bytearray_set256(
                        bytearray_new(0),
                        0,
                        uint(topFrame.runningAs)
                    );

                    evmTracer_pushReturnRevert(
                        const::TxResultCode_success,
                        returnData,
                        gasUsage.gasUsed,
                    );

                    // emit queued sends
                    let updatedEvmLogs = sendQueuedMessages(topFrame.sendQueue, topFrame.evmLogs);

                    // Emit log item reporting success
                    emitTxReceipt(
                        globalCurrentTxRequest.incomingRequest,
                        const::TxResultCode_success,
                        Some(returnData),
                        Some(updatedEvmLogs),
                        Some(gasUsage),
                        feeStats,
                        true,
                        globalCurrentTxRequest.originalCaller,
                    );

                    // increment global contract count
                    setGlobalAccountStore(accountStore_changeNumContracts(getGlobalAccountStore(), int(1)));
                } else {
                    let updatedEvmLogs = xif (! globalCurrentTxRequest.nonMutating) {
                        // It was a successful tx (not a non-mutating call).
                        // We'll commit the results of the tx

                        if let Some(reqAndLogs) = globalCurrentRetryableRequest {
                            emitTxReceipt(
                                reqAndLogs.req,
                                const::TxResultCode_success,
                                Some(returnData),
                                Some(reqAndLogs.evmLogs),
                                None<GasUsage>,
                                txFeeStats_zero(),
                                false,
                                globalCurrentTxRequest.originalCaller,
                            );
                            returnData = bytearray_new(0);   // outer tx should have no returndata
                            globalCurrentRetryableRequest = None<IncomingRequestAndLogs>;
                        }

                        // Save cached account back to main accountStore
                        let acctStore = accountStore_set(
                            topFrame.accountStore,
                            topFrame.runningAs,
                            topFrame.runningAsAccount
                        );

                        // Commit the self-destruct events that were requested during the call.
                        let sdq = topFrame.selfDestructQueue;
                        while( ! queue_isEmpty(sdq) ) {
                            let (q, item) = queue_get(sdq)?;
                            sdq = q;
                            let victim = unsafecast<address>(item);
                            acctStore = accountStore_changeNumContracts(
                                accountStore_destroyAccount(acctStore, victim),
                                -1s,
                            );
                        }

                        // emit queued sends
                        let updEvmLogs = sendQueuedMessages(topFrame.sendQueue, topFrame.evmLogs);

                        // Save results back to the main globalAccountStore
                        if let Some(uas) = deferredPayments_apply(acctStore, deferredPayments) {
                            acctStore = uas;
                        }
                        setGlobalAccountStore(acctStore);

                        updEvmLogs
                    } else {
                        topFrame.evmLogs
                    };

                    evmTracer_pushReturnRevert(
                        const::TxResultCode_success,
                        returnData,
                        gasUsage.gasUsed,
                    );

                    // Emit a log item reporting success
                    emitTxReceipt(
                        globalCurrentTxRequest.incomingRequest,
                        const::TxResultCode_success,
                        Some(returnData),
                        Some(updatedEvmLogs),
                        Some(gasUsage),
                        feeStats,
                        true,
                        globalCurrentTxRequest.originalCaller,
                    );
                }
            } else {
                evmTracer_pushReturnRevert(
                    const::TxResultCode_revert,
                    returnData,
                    gasUsage.gasUsed,
                );
                // Emit a log item reporting failure
                emitTxReceipt(
                    globalCurrentTxRequest.incomingRequest,
                    const::TxResultCode_revert,
                    None<ByteArray>,
                    None<EvmLogs>,
                    Some(gasUsage),
                    feeStats,
                    true,
                    globalCurrentTxRequest.originalCaller,
                );
                if let Some(uas) = deferredPayments_apply(getGlobalAccountStore(), deferredPayments) {
                    setGlobalAccountStore(uas);
                }
            }
        } else {
            // Request failed
            let (_, gasUsage, feeStats, payments) = gasAccounting_endTxCharges(int(0), globalCurrentTxRequest.feeStats)?;
            if let Some(uas) = deferredPayments_apply(getGlobalAccountStore(), payments) {
                setGlobalAccountStore(uas);
            }
            if let Some(msg) = topFrame.sendOnFailure {
                outbox_append(msg);
            }
            evmTracer_pushReturnRevert(
                const::TxResultCode_revert,
                xif (globalCurrentTxRequest.isConstructor) { bytearray_new(0) } else { returnData },
                gasUsage.gasUsed,
            );
            emitTxReceipt(
                globalCurrentTxRequest.incomingRequest,
                const::TxResultCode_revert,
                xif (globalCurrentTxRequest.isConstructor) { None<ByteArray> } else { Some(returnData) },
                None<EvmLogs>,
                Some(gasUsage),
                feeStats,
                true,
                globalCurrentTxRequest.originalCaller,
            );
        }

        // Now discard any EVM and AVM stack contents and jump back to the main run loop.
        globalCallStack = None<EvmCallFrame>;
        emptyAvmStack();
        restoreAuxStackAndCall(
            xif let Some(target) = jumpTargetIfTxEnd { target } else { mainRunLoop },
            None<Stack>,
            true
        );  // should never return
        evmCallStack_callHitError(9);
        panic
    };
}

public impure func evmCallStack_callRetryableTxAndReturn(rtx: RetryableTx) -> (bool, ByteArray) {
    return xif let Some(topFrame) = globalCallStack {
        // Save parts of the callframe, then overwrite them before making the call,
        //     in order to impersonate the original caller.
        let savedRunningAs = topFrame.runningAs;
        let savedMemory = topFrame.memory;
        let savedEvmLogs = topFrame.evmLogs;

        let theFrame = evmCallFrame_setRunningAsAddress(topFrame, rtx.sender);
        theFrame = theFrame with {
            memory: rtx.calldata
        } with {
            evmLogs: evmlogs_empty()
        };
        globalCallStack = Some(theFrame);

        globalCurrentRetryableRequest = Some(struct {
            req: rtx.l1Message with {
                arbBlockNumber: inbox_currentArbBlockNumber()
            } with {
                ethBlockNumber: inbox_currentEthBlockNumber()
            } with {
                timestamp: inbox_currentTimestamp()
            },
            evmLogs: evmlogs_empty(),
            originalSender: retryableTx_getOriginalSender(rtx),
        });

        let retCode = evmOp_callAndReturn(
            1000000000000000000,    // give all of our gas to the callee
            rtx.destination,
            rtx.callvalue,
            0,
            bytearray_size(rtx.calldata),
            0,
            0,
            false,
            Some(rtx.callvalueEscrowKey),
            None<AvmCodePoint>,
        );

        xif let Some(newTopFrame) = globalCallStack {
            // save evm logs to emit in inner tx's receipt
            if let Some(retryableReq) = globalCurrentRetryableRequest {
                globalCurrentRetryableRequest = Some(
                    retryableReq with { evmLogs: newTopFrame.evmLogs }
                );
            }

            // restore saved callframe info
            newTopFrame = newTopFrame with {
                memory: savedMemory
            } with {
                evmLogs: savedEvmLogs
            };
            newTopFrame = evmCallFrame_setRunningAsAddress(newTopFrame, savedRunningAs);
            globalCallStack = Some(newTopFrame);
            (retCode, xif let Some(retInfo) = newTopFrame.returnInfo { retInfo.data } else { bytearray_new(0) })
        } else {
            (false, bytearray_new(0))
        }
    } else {
        (false, bytearray_new(0))
    };
}

func ethTransferOrGrant(
    acctStore: AccountStore,
    payer: address,
    recipient: address,
    amount: uint,
    callvalueEscrowKey: option<uint>,
) -> option<AccountStore> {
    return xif let Some(escrowKey) = callvalueEscrowKey {
        accountStore_payFromEscrow(acctStore, escrowKey, recipient, amount)
    } else {
        accountStore_transferEthBalance(acctStore, payer, recipient, amount)
    };
}

func storageReclaimedForSDQ(topFrame: EvmCallFrame) -> uint {
    let q = topFrame.selfDestructQueue;
    let ret = 0;
    return xloop {
        if let Some(res) = queue_get(q) {
            let (uq, rawItem) = res;
            q = uq;
            let victim = unsafecast<address>(rawItem);
            if let Some(sz) = account_getStorageSize(evmCallFrame_getAccount(topFrame, victim)) {
                ret = ret + sz;
            }
        } else {
            return ret;
        }
    };
}

func saveAvmStack() -> Stack {
    // pop all items from the AVM stack, push them into a Stack
    let ret = stack_new();
    while( ! asm() bool { stackempty } ) {
        let stackTop = asm() any { };  // pop value from AVM stack
        ret = stack_push(ret, stackTop);
    }
    return ret;
}

public func emptyAvmStack() {
    // discard everything on the AVM stack
    while( ! asm() bool { stackempty } ) {
        let _ = asm() any { };  // pop value from AVM stack
    }
}

func restoreAvmStack(savedStack: Stack) {
    // restore AVM stack state, from a Stack that was created earlier by saveAvmStack
    while( ! stack_isEmpty(savedStack) ) {
        if let Some(res) = stack_pop(savedStack) {
            let (newStack, top) = res;
            savedStack = newStack;
            asm (top,) { };  // push top onto AVM stack
        }
    }
}

public impure func snapshotAuxStack() -> Stack {
    // Save a copy everything from the auxstack, except the current callframe (which is on top).
    // Keep the callframe on top of the auxstack (except during asm blocks)
    //       in case the code generator wants to use the callframe
    let ret = stack_new();
    while ( ! asm() bool { auxpop auxstackempty swap1 auxpush }) {
        ret = stack_push(
            ret,
            asm() any {
                auxpop
                auxpop
                swap1
                auxpush
            }
        );
    }

    // Now restore everything that we popped from the aux stack, so the aux stack ends up where it started.
    let savedAuxStack = ret;
    while( ! stack_isEmpty(savedAuxStack)) {
        if let Some(res) = stack_pop(savedAuxStack) {
            let (ustack, val) = res;
            savedAuxStack = ustack;
            asm(val,) {
                auxpop
                swap1
                auxpush
                auxpush
            };
        } else {
            // can't happen, because we just verified that stack isn't empty
            evmCallStack_callHitError(19);  // shouldn't ever return
        }
    }

    // discard the first item saved to the saved aux stack, because it belongs to this function
    return stack_discardDeepestItems(ret, 1);
}

public impure func cleanAvmAuxStackAndCall(funcToCall: impure func(), pushFakeReturnAddress: bool) {
    restoreAuxStackAndCall(funcToCall, None<Stack>, pushFakeReturnAddress);
}

public impure func restoreAuxStackAndCall(
    funcToCall: impure func(),
    maybeSavedAuxStack: option<Stack>,
    pushFakeReturnAddress: bool
) {
    // This restores a saved version of the AvmAuxStack, then calls funcToCall.
    // funcToCall must never try to return.

    // Discard everything from the auxstack, except the current callframe (which is on top).
    // Keep the callframe on top of the auxstack (except during asm blocks)
    //       in case the code generator wants to use the callframe
    while ( ! asm() bool { auxpop auxstackempty swap1 auxpush }) {
        asm() {
            auxpop
            auxpop
            pop
            auxpush
        };
    }

    if let Some(savedAuxStack) = maybeSavedAuxStack {
        // Restore the saved AuxStack, leaving only the current callframe on top.
        // Keep the callframe on top of the auxstack (except during asm blocks)
        while( ! stack_isEmpty(savedAuxStack)) {
            if let Some(res) = stack_pop(savedAuxStack) {
                let (ustack, val) = res;
                savedAuxStack = ustack;
                asm(val,) {
                    auxpop
                    swap1
                    auxpush
                    auxpush
                };
            } else {
                // can't happen, because we just verified that stack isn't empty
                evmCallStack_callHitError(19);  // shouldn't ever return
            }
        }
    }

    // Discard our callframe, push a fake return address, and jump to funcToCall
    // funcToCall will think somebody called it normally, with the restored auxstack
    if (pushFakeReturnAddress) {
        asm(funcToCall,) {
            auxpop
            pop
            [0] swap1  // bogus return address, which funcToCall will save in its callframe but will never use
            jump
        };
    } else {
        asm(funcToCall,) {
            auxpop
            pop
            jump
        };
    }
}

public impure func evmCallStack_topFrame() -> option<EvmCallFrame> {
    return globalCallStack;
}

public impure func evmCallStack_oldestCallFrame() -> option<EvmCallFrame> {
    let frame = globalCallStack?;
    return xloop {
        if let Some(parent) = frame.parent {
            frame = parent;
        } else {
            return Some(frame);
        }
    };
}

public func evmCallFrame_constructorIsOnStackForAddress(frame: EvmCallFrame, addr: address) -> bool {
    return xloop {
        if ( (frame.isConstructor) && (frame.runningAs == addr) ) {
            return true;
        }
        if let Some(parent) = frame.parent {
            frame = parent;
        } else {
            return false;
        }
    };
}

public impure func evmCallStack_touchMemoryOffset(offset: uint) {
    if let Some(topFrame) = globalCallStack {
        let prev = topFrame.maxMemoryOffsetTouched;
        if (offset > prev) {
            let delta =   (safeMul(offset, offset) / const::Charging_MemoryChargeDenominator)
                        - (safeMul(prev, prev) / const::Charging_MemoryChargeDenominator);
            globalCallStack = Some(topFrame with { maxMemoryOffsetTouched: offset });

            // deduct the gas difference from current gas remaining, but add it back to the gas pool because it's not computation
            let gas = asm() uint { getgas } / const::Charging_ArbGasPerAvmGas;
            if (delta < gas) {
                gasAccounting_addToGasPool(delta);
                asm(const::Charging_ArbGasPerAvmGas * (gas - delta),) { setgas };
            } else {
                gasAccounting_addToGasPool(gas);
                asm(1,) { setgas };      // ensure we'll run out of gas immediately
            }
        }
    }
}

public impure func evmCallStack_runningCodeFromAccount() -> option<Account> {
    return Some(evmCallFrame_runningCodeFromAccount(globalCallStack?));
}

public impure func evmCallStack_getTopFrameReturnData() -> option<ByteArray> {
    return Some(((globalCallStack?).returnInfo?).data);
}

public impure func evmCallStack_getAccount(addr: address) -> option<Account> {
    let topFrame = globalCallStack?;
    return xif (topFrame.runningAs == addr) {
        Some(topFrame.runningAsAccount)
    } else {
        Some(accountStore_get(topFrame.accountStore, addr))
    };
}

public impure func evmCallStack_setAccount(addr: address, acct: Account) -> bool {
    return xif let Some(topFrame) = globalCallStack {
        if (topFrame.runningAs == addr) {
            globalCallStack = Some(topFrame with { runningAsAccount: acct });
        } else {
            globalCallStack = Some(topFrame with {
                accountStore: accountStore_set(topFrame.accountStore, addr, acct)
            });
        }
        true
    } else {
        false
    };
}

public impure func evmCallStack_getAccountInCurrentContext(addr: address) -> Account {
    return xif let Some(topFrame) = globalCallStack {
        evmCallFrame_getAccount(topFrame, addr)
    } else {
        accountStore_get(getGlobalAccountStore(), addr)
    };
}

public impure func evmCallStack_setAccountInCurrentContext(addr: address, acct: Account) {
    if ( ! evmCallStack_setAccount(addr, acct)) {
        setGlobalAccountStore(
            accountStore_set(
                getGlobalAccountStore(),
                addr,
                acct,
            )
        );
    }
}

public impure func evmCallStack_getAccountStoreInCurrentContext() -> AccountStore {
    return xif let Some(topFrame) = globalCallStack {
        accountStore_set(topFrame.accountStore, topFrame.runningAs, topFrame.runningAsAccount)
    } else {
        getGlobalAccountStore()
    };
}

public impure func evmCallStack_setAccountStoreInCurrentContext(acctStore: AccountStore) {
    if let Some(topFrame) = globalCallStack {
        globalCallStack = Some(
            topFrame with {
                accountStore: acctStore
            } with {
                runningAsAccount: accountStore_get(acctStore, topFrame.runningAs)
            }
        );
    } else {
        setGlobalAccountStore(acctStore);
    }
}

public impure func _evmCallStack_transferEthInCurrentContext(from: address, to: address, amount: uint) -> option<()> {
    if let Some(topFrame) = globalCallStack {
        globalCallStack = Some(evmCallFrame_transferEth(topFrame, from, to, amount)?);
    } else {
        setGlobalAccountStore(accountStore_transferEthBalance(getGlobalAccountStore(), from, to, amount)?);
    }
    return Some(());
}

public impure func evmCallStack_getRealCallerInfo(levelsToSubtract: uint) -> option<(address, bool)> {
    // ignoring the top <levelsToSubtract> levels of the EVM call stack,
    //     return (the sender's address without L1 contract address aliasing applied, whether it was aliased

    let stackDepth = evmCallStack_stackDepth();
    if (stackDepth == 1+levelsToSubtract) {
        // top-level call from L1
        return Some((
            globalCurrentTxRequest.originalCaller,
            (globalCurrentTxRequest.caller != globalCurrentTxRequest.originalCaller)
        ));
    } elseif (stackDepth == 2+levelsToSubtract) {
        if let Some(gcrr) = globalCurrentRetryableRequest {
            return Some((gcrr.originalSender, (gcrr.req.sender != gcrr.originalSender)));
        }
    }

    let frame = globalCallStack?;
    while (levelsToSubtract > 0) {
        frame = frame.parent?;
        levelsToSubtract = levelsToSubtract - 1;
    }
    return Some((frame.caller, false));
}

public impure func evmCallStack_changeNumAccounts(delta: int) {
    if let Some(topFrame) = globalCallStack {
        globalCallStack = Some(
            topFrame with { accountStore: accountStore_changeNumContracts(topFrame.accountStore, delta) }
        );
    }
    // if not in a stack frame (which shouldn't happen), ignore the problem, the only consequence is inaccurate stats
}

public impure func evmCallStack_getTopFrameMemoryOrDie() -> ByteArray {
    return xif let Some(topFrame) = globalCallStack {
        topFrame.memory
    } else {
        evmCallStack_callHitError(10);
        panic
    };
}

public impure func evmCallStack_setTopFrameMemory(mem: ByteArray) -> bool {
    evmCallStack_touchMemoryOffset(bytearray_size(mem));
    return xif let Some(topFrame) = globalCallStack {
        globalCallStack = Some(topFrame with { memory: mem });
        true
    } else {
        false
    };
}

public impure func evmCallStack_queueMessage(msg: any) {
    if let Some(topFrame) = globalCallStack {
        globalCallStack = Some(
            topFrame with {
                sendQueue: queue_put(topFrame.sendQueue, msg)
            }
        );
        return;
    }

    evmCallStack_callHitError(14);
}

public impure func evmCallStack_transferEthFromCurrent(
    dest: address,
    amount: uint,
) -> option<()> {
    let topFrame = globalCallStack?;
    globalCallStack = Some(evmCallFrame_transferEth(topFrame, topFrame.runningAs, dest, amount)?);
    return Some(());
}

public impure func evmCallStack_transferEthFromEscrow(escrowKey: uint, to: address, amount: uint) -> option<()> {
    let topFrame = globalCallStack?;
    if (to == topFrame.runningAs) {
        let acctStore = accountStore_set(topFrame.accountStore, topFrame.runningAs, topFrame.runningAsAccount);
        acctStore = accountStore_payFromEscrow(acctStore, escrowKey, to, amount)?;
        globalCallStack = Some(topFrame with {
            accountStore: acctStore
        } with {
            runningAsAccount: accountStore_get(acctStore, topFrame.runningAs)
        });
    } else {
        globalCallStack = Some(topFrame with {
            accountStore: accountStore_payFromEscrow(topFrame.accountStore, escrowKey, to, amount)?
        });
    }
    return Some(());
}


public impure func evmCallStack_setTopFrameStorageCell(offset: uint, value: uint) -> option<()> {
    let topFrame = globalCallStack?;
    let storageSizeBefore = account_getStorageSize(topFrame.runningAsAccount)?;
    let runningAsAccount = account_setStorageCell(
        topFrame.runningAsAccount,
        offset,
        value
    )?;
    globalCallStack = Some(topFrame with {
        runningAsAccount: runningAsAccount
    } with {
        storageDelta: topFrame.storageDelta + int(account_getStorageSize(runningAsAccount)?) - int(storageSizeBefore)
    });
    return Some(());
}

public impure func evmCallStack_addToCurrentStorageDelta(delta: int) -> option<()> {
    let topFrame = globalCallStack?;
    topFrame = topFrame with {
        storageDelta: topFrame.storageDelta + delta
    };
    globalCallStack = Some(topFrame);
    return Some(());
}

public impure func evmCallStack_addEvmLogToCurrent(item: any) -> option<()> {
    let topFrame = globalCallStack?;
    item = asm(0, item, topFrame.runningAs) any { tset };
    let newLogs = evmlogs_appendAny(topFrame.evmLogs, item);
    globalCallStack = Some(
        topFrame with {
            evmLogs: newLogs
        }
    );
    return Some(());
}

public impure func evmCallStack_selfDestructCurrentAccount(recipient: address) {
    if let Some(topFrame) = globalCallStack {
        let destructee = topFrame.runningAs;
        let amount = account_getBalance(topFrame.runningAsAccount);
        if (amount > 0) {
            // give amount to the correct party, according to the yellow paper's rules
            if (recipient == destructee) {
                // yellow paper says to burn the funds; we'll give them to the chain's reserve fund
                recipient = getAddressForReserveFundsDeposit();
            }
            topFrame = xif let Some(tf) = evmCallFrame_transferEth(topFrame, destructee, recipient, amount) {
                tf
            } else {
                evmCallStack_callHitError(30);
                //NOTREACHED
                topFrame
            };
        }

        globalCallStack = Some(
            topFrame with {
                selfDestructQueue: queue_put(topFrame.selfDestructQueue, destructee)
            }
        );
    }
}

public func evmCallFrame_runningAsAddress(frame: EvmCallFrame) -> address {
    return frame.runningAs;
}

public func evmCallFrame_setRunningAsAddress(frame: EvmCallFrame, addr: address) -> EvmCallFrame {
    let acctStore = accountStore_set(frame.accountStore, frame.runningAs, frame.runningAsAccount);
    return frame with {
        runningAs: addr
    } with {
        runningAsAccount: accountStore_get(acctStore, addr)
    } with {
        accountStore: acctStore
    };
}

public func evmCallFrame_runningAsAccount(frame: EvmCallFrame) -> Account {
    return frame.runningAsAccount;
}

public func evmCallFrame_runningCodeFromAccount(frame: EvmCallFrame) -> Account {
    return evmCallFrame_getAccount(frame, frame.runningCodeFrom);
}

public func evmCallFrame_getCaller(frame: EvmCallFrame) -> address {
    return frame.caller;
}

public func evmCallFrame_getAccount(frame: EvmCallFrame, addr: address) -> Account {
    return xif (addr == frame.runningAs) {
        frame.runningAsAccount
    } else {
        accountStore_get(frame.accountStore, addr)
    };
}

public func evmCallFrame_transferEth(frame: EvmCallFrame, from: address, to: address, amount: uint) -> option<EvmCallFrame> {
    return xif ((from == frame.runningAs) || (to == frame.runningAs)) {
        // runningAs account is involved, so we need to save and restore the cached copy of it
        let acctStore = accountStore_set(frame.accountStore, frame.runningAs, frame.runningAsAccount);
        acctStore = accountStore_transferEthBalance(acctStore, from, to, amount)?;
        Some(frame with {
            accountStore: acctStore
        } with {
            runningAsAccount: accountStore_get(acctStore, frame.runningAs)
        })
    } else {
        Some(frame with { accountStore: accountStore_transferEthBalance(frame.accountStore, from, to, amount)? })
    };
}

public func evmCallFrame_getMemory(frame: EvmCallFrame) -> ByteArray {
    return frame.memory;
}

public func evmCallFrame_getCallvalue(frame: EvmCallFrame) -> uint {
    return frame.callvalue;
}

public func evmCallFrame_getCalldata(frame: EvmCallFrame) -> ByteArray {
    return frame.calldata;
}

public func evmCallFrame_shouldRevertOnStorageWrite(frame: EvmCallFrame) -> bool {
    return frame.revertOnStorageWrite;
}

public func _evmCallFrame_getEvmLogs(frame: EvmCallFrame) -> EvmLogs {
    return frame.evmLogs;
}

public func _evmCallFrame_getResumeCodePoint(frame: EvmCallFrame) -> option<func()> {
    return Some((frame.resumeInfo?).codePoint);
}

public func _evmCallFrame_getSelfDestructQueue(frame: EvmCallFrame) -> Queue {
    return frame.selfDestructQueue;
}

public func _evmCallFrame_getParent(frame: EvmCallFrame) -> option<EvmCallFrame> {
    return frame.parent;
}

public impure func evmCallStack_callHitError(errInfo: any) {
    asm((664, errInfo),) { debugprint };
    if (evmCallStack_returnFromCall(false, 0, 0, None<impure func()>) == None<()>) {
        // something has gone seriously wrong with the evmCallStack
        // recover by clearing the evmCallStack, refunding unused gas, and jumping back to the error handler
        globalCallStack = None<EvmCallFrame>;
        let _ = gasAccounting_endTxCharges(int(0), txFeeStats_zero());

        errorHandler();  // should never return
        panic;
    }
}

func _getLastFrame(topFrame: EvmCallFrame) -> EvmCallFrame {
    return xloop {
        if let Some(tf) = topFrame.parent {
            topFrame = tf;
        } else {
            return topFrame;
        }
    };
}
