//
// Copyright 2020, Offchain Labs, Inc. All rights reserved.
//

use accounts::getGlobalAccountStore;
use accounts::setGlobalAccountStore;
use accounts::accountStore_transferEthBalance;

use pluggables::ModuleFunction;
use pluggables::installPluggable;

use std::bytearray::ByteArray;
use std::bytestream::ByteStream;
use std::bytestream::bytestream_new;
use std::bytestream::bytestream_getByte;
use std::bytestream::bytestream_get256;

use std::queue::Queue;
use std::queue::queue_new;
use std::queue::queue_put;
use std::queue::queue_get;
use std::queue::queue_getOrDie;
use std::queue::queue_nth;
use std::queue::queue_set_nth;


public impure func rollupTracker_init() {
    if let Some(_) = installPluggable(
        const::PluggableModuleID_rollupTracker,
        unsafecast<ModuleFunction>(rollupTracker_processEvent),
        false,
    ) {
        return;
    } else {
        panic;
    }
}

public func rollupTracker_processEvent(
    msg: ByteArray,
    tracker: RollupTracker,
    neverBeenCalled: bool,
    pluggableCallback: impure func(uint, any) -> option<any>,
) -> (any, any) {   // returns (success: bool, updatedTracker: RollupTracker)
    if (neverBeenCalled) {
        tracker = rollupTracker_initModule();
    }
    return xif let Some(updatedTracker) = processEventOrError(bytestream_new(msg), tracker) {
        (true, updatedTracker)
    } else {
        (false, tracker)
    };
}

func processEventOrError(stream: ByteStream, tracker: RollupTracker) -> option<RollupTracker> {
    let (us, eventType) = bytestream_getByte(stream)?;
    stream = us;

    return xif (eventType == const::ProtoEvent_createNode) {
        let (us, heightL2) = bytestream_get256(stream)?;
        let (us, prev) = bytestream_get256(us)?;
        let (us, heightL1) = bytestream_get256(us)?;
        let (us, deadlineL1) = bytestream_get256(us)?;
        let (us, asserterUint) = bytestream_get256(us)?;
        rollupTracker_addNode(
            tracker,
            heightL2,
            prev,
            heightL1,
            deadlineL1,
            address(asserterUint)
        )
    } elseif (eventType == const::ProtoEvent_confirmNode) {
        let (us, heightL2) = bytestream_get256(stream)?;
        rollupTracker_confirmNode(tracker, heightL2)
    } elseif (eventType == const::ProtoEvent_rejectNode) {
        let (us, heightL2) = bytestream_get256(stream)?;
        rollupTracker_rejectNode(tracker, heightL2)
    } elseif (eventType == const::ProtoEvent_newStake) {
        let (us, heightL2) = bytestream_get256(stream)?;
        let (us, stakerUint) = bytestream_get256(us)?;
        let (us, stakeTimeL1) = bytestream_get256(us)?;
        rollupTracker_newStake(
            tracker,
            heightL2,
            address(stakerUint),
            stakeTimeL1
        )
    } elseif (eventType == const::ProtoEvent_claimNode) {
        let (us, heightL2) = bytestream_get256(stream)?;
        let (us, claimerUint) = bytestream_get256(us)?;
        rollupTracker_claimNode(tracker, heightL2, address(claimerUint))
    } elseif (eventType == const::ProtoEvent_debug) {
        _debug(tracker);
        Some(tracker)
    } else {
        None<RollupTracker>
    };
}

type RollupNode = struct {
    heightL2: uint,
    prev: option<uint>,
    heightL1: uint,
    deadlineL1: uint,
}

type RollupTracker = struct {
    nodes: RollupSlidingWindow,
    latestConfirmed: uint,
    firstUnresolved: uint,
}

func rollupTracker_initModule() -> RollupTracker {
    let genesisNode = struct {
        heightL2: 0,
        prev: None<uint>,
        heightL1: 0,
        deadlineL1: 1,
    };
    return struct {
        nodes: rsw_new(genesisNode),
        latestConfirmed: 0,
        firstUnresolved: 1,
    };
}

func rollupTracker_get(tracker: RollupTracker, heightL2: uint) -> option<RollupNode> {
    return rsw_get(tracker.nodes, heightL2);
}

func rollupTracker_confirmNode(tracker: RollupTracker, heightL2: uint) -> option<RollupTracker> {
    if (heightL2 != tracker.firstUnresolved) {    return None;    }

    let nodes = tracker.nodes;
    let node = rsw_get(nodes, heightL2)?;

    // now eliminate nodes that aren't needed anymore
    // these will include the old latestConfirmed, and any already rejected nodes that follow it
    while(rsw_getNextOut(nodes) < heightL2) {
        nodes = rsw_dropOne(nodes)?;
    }

    tracker = tracker with {
        latestConfirmed: heightL2
    } with {
        firstUnresolved: heightL2 + 1
    } with {
        nodes: nodes
    };

    return Some(tracker);
}

func rollupTracker_rejectNode(tracker: RollupTracker, heightL2: uint) -> option<RollupTracker> {
    if (heightL2 != tracker.firstUnresolved) {    return None;    }

    return Some(tracker with {
        nodes: rsw_dropOne(tracker.nodes)?
    } with {
        firstUnresolved: tracker.firstUnresolved + 1
    });
}

func rollupTracker_newStake(
    tracker: RollupTracker,
    heightL2: uint,
    staker: address,
    stakeTimeL1: uint
) -> option<RollupTracker> {
    let node = rsw_get(tracker.nodes, heightL2)?;
    if (stakeTimeL1 > node.deadlineL1) {
        return None;
    }

    let _ = rsw_get(tracker.nodes, node.prev?)?;

    return Some(tracker);
}

func rollupTracker_addNode(
    tracker: RollupTracker,
    heightL2: uint,
    prev: uint,
    heightL1: uint,
    deadlineL1: uint,
    asserter: address,
) -> option<RollupTracker> {
    let _ = rsw_get(tracker.nodes, prev)?;
    return Some(tracker with {
        nodes: rsw_append(
            tracker.nodes,
            struct {
                heightL2: heightL2,
                prev: Some(prev),
                heightL1: heightL1,
                deadlineL1: deadlineL1,
            }
        )?
    });
}

// claiming a node means taking the active validator role from somebody else, with a reduced payment
func rollupTracker_claimNode(
    tracker: RollupTracker,
    heightL2: uint,
    claimer: address
) -> option<RollupTracker> {
    return Some(tracker);
}

type RollupSlidingWindow = struct {
    contents: Queue,
    nextOut: uint,
    nextIn: uint,
}

func rsw_new(genesisNode: RollupNode) -> RollupSlidingWindow {
    return struct {
        contents: queue_put(queue_new(), genesisNode),
        nextOut: 0,
        nextIn: 1,
    };
}

func rsw_getNextIn(rsw: RollupSlidingWindow) -> uint {
    return rsw.nextIn;
}

func rsw_getNextOut(rsw: RollupSlidingWindow) -> uint {
    return rsw.nextOut;
}

func rsw_getNumNodes(rsw: RollupSlidingWindow) -> uint {
    return rsw.nextIn - rsw.nextOut;
}

func rsw_confirmIndex(rsw: RollupSlidingWindow, index: uint) -> option<()> {
    return xif ((index >= rsw.nextOut) && (index < rsw.nextIn)) {
        Some(())
    } else {
        None<()>
    };
}

func rsw_get(rsw: RollupSlidingWindow, index: uint) -> option<RollupNode> {
    if ((index < rsw.nextOut) || (index >= rsw.nextIn)) {
        return None;
    }
    return unsafecast<option<RollupNode>>(queue_nth(rsw.contents, index-rsw.nextOut));  // correctly returns None if subtraction underflows
}

func rsw_set(rsw: RollupSlidingWindow, index: uint, node: RollupNode) -> option<RollupSlidingWindow> {
    return Some(
        rsw with {
            contents: queue_set_nth(
                rsw.contents,
                index-rsw.nextOut,  // if underflow, queue_set_nth returns None, so this func correctly returns None
                node,
            )?
        }
    );
}

func rsw_append(rsw: RollupSlidingWindow, node: RollupNode) -> option<RollupSlidingWindow> {
    if (node.heightL2 != rsw.nextIn) {
        return None;
    }

    return Some(
        rsw with {
            contents: queue_put(rsw.contents, node)
        } with {
            nextIn: rsw.nextIn + 1
        }
    );
}

func rsw_dropOne(rsw: RollupSlidingWindow) -> option<RollupSlidingWindow> {
    let (uq, _) = queue_get(rsw.contents)?;
    return Some(
        rsw with {
            contents: uq
        } with {
            nextOut: rsw.nextOut + 1
        }
    );
}

func rsw_verifyIsLeaf(rsw: RollupSlidingWindow, heightL2: uint) -> option<()> {
    let _ = rsw_confirmIndex(rsw, heightL2)?;

    let i = heightL2 + 1;
    while (i < rsw.nextIn) {
        let node = rsw_get(rsw, i)?;
        if let Some(h) = node.prev {
            if (h == heightL2) {
                return None;
            }
        }
        i = i+1;
    }
    return Some(());
}

func _debug(tracker: RollupTracker) {
    let height = tracker.nodes.nextOut;
    let q = tracker.nodes.contents;
    loop {
        if let Some(res) = queue_get(q) {
            let (uq, node) = res;
            q = uq;
            asm((9000, height, node),) { debugprint };
            height = height+1;
        } else {
            return;
        }
    }
}