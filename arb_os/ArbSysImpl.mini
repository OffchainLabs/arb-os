//
// Copyright 2020, Offchain Labs, Inc. All rights reserved.
//

use evmCallStack::EvmCallFrame;
use accounts::Account;
use std::bytearray::ByteArray;
use std::bytearray::MarshalledBytes;
use std::stack::Stack;
use accounts::AccountStore;
use std::bytestream::ByteStream;

use chainParameters::chainParams_chainId;

use evmCallStack::evmCallStack_stackDepth;
use evmCallStack::evmCallStack_topFrame;
use evmCallStack::evmCallStack_setAccount;
use evmCallStack::evmCallStack_getTopFrameMemoryOrDie;
use evmCallStack::evmCallStack_setTopFrameMemory;
use evmCallStack::evmCallStack_callHitError;
use evmCallStack::evmCallStack_addEvmLogToCurrent;
use evmCallStack::evmCallStack_addStorageCharge;

use evmCallStack::evmCallFrame_getCalldata;
use evmCallStack::evmCallFrame_getCallvalue;
use evmCallStack::evmCallFrame_getAccount;
use evmCallStack::evmCallFrame_runningAsAddress;
use evmCallStack::evmCallFrame_runningAsAccount;
use evmCallStack::evmCallFrame_getParent;
use evmCallStack::evmCallFrame_getCaller;

use evmOps::evmOp_log4;
use evmOps::evmOp_return;
use evmOps::evmOp_revert_knownCodePc;
use evmOps::evmOp_sload;
use evmOps::evmOp_sstore;

use accounts::account_getAddress;
use accounts::account_getNextSeqNum;
use accounts::account_getStorageCell;
use accounts::account_deductFromEthBalance;
use accounts::account_setBlsKey;
use accounts::account_getBlsKey;
use accounts::account_getAggregatorDecompressionState;
use accounts::account_setAggregatorDecompressionState;

use std::bytearray::bytearray_new;
use std::bytearray::bytearray_size;
use std::bytearray::bytearray_getByte;
use std::bytearray::bytearray_setByte;
use std::bytearray::bytearray_get256;
use std::bytearray::bytearray_set256;
use std::bytearray::bytearray_extract;
use std::bytearray::bytearray_extract;
use std::bytearray::bytearray_copy;

use std::bytestream::bytestream_new;
use std::bytestream::bytestream_bytesReadSoFar;

use inbox::inbox_currentArbBlockNumber;
use inbox::inbox_currentEthBlockNumber;
use inbox::inbox_currentTimestamp;

use std::stack::stack_pop;

use output::queueMessageForSend;

use decompression::getGlobalAddressTable;
use decompression::setGlobalAddressTable;
use decompression::addressTable_size;
use decompression::addressTable_getByAddressAlloc;
use decompression::addressTable_getByAddress;
use decompression::addressTable_getByIndex;
use decompression::decompressAddress;
use decompression::getFromFunctionTable;
use decompression::functionTableSize;
use decompression::getFromFunctionTable;

use std::rlp::rlp_encodeUint;
use std::rlp::rlp_encodeAddress;

use std::bls::bls_makeKey;
use std::bls::bls_marshalPublicKey;

use decompression::parseAggregatorFunctionTable;

use codeSegment::pcTableForCode;

use ArbOwnerImpl::requireCalledByOwner;

public impure func arbAddressTable_txcall() {
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        if (bytearray_size(calldata) < 4) {
            evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 0, 0, 0);
        }
        let funcCode = getFuncCode(calldata);

        if(funcCode == 0x4420e486) {
            arbAddressTable_register(topFrame, calldata);
        } elseif(funcCode == 0xd4b6b5da) {
            arbAddressTable_lookup(topFrame, calldata);
        } elseif(funcCode == 0xa5025222) {
            arbAddressTable_addressExists(topFrame, calldata);
        } elseif(funcCode == 0x949d225d) {
            arbAddressTable_size(topFrame, calldata);
        } elseif(funcCode == 0x8a186788) {
            arbAddressTable_lookupIndex(topFrame, calldata);
        } elseif(funcCode == 0x31862ada) {
            arbAddressTable_decompress(topFrame, calldata);
        } elseif(funcCode == 0xf6a455a2) {
            arbAddressTable_compress(topFrame, calldata);
        } else {
            // unrecognized function code
            evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 1, 0, 0);
        }
    } else {
        // this shouldn't happen -- should always be called in an EVM tx
        evmCallStack_callHitError(17);
    }
}

public impure func arbBLS_txcall() {
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        if (bytearray_size(calldata) < 4) {
            evmOp_revert_knownCodePc(address(const::Address_ArbBLS), 0, 0, 0);
        }
        let funcCode = getFuncCode(calldata);
        if (funcCode == 0x375a7c7f) {
            arbBLS_register(topFrame, calldata);
        } elseif(funcCode == 0x857cdbb8) {
            arbBLS_getPublicKey(topFrame, calldata);
        } else {
            // unrecognized function code
            evmOp_revert_knownCodePc(address(const::Address_ArbBLS), 1, 0, 0);
        }
    } else {
        // this shouldn't happen -- should always be called in an EVM tx
        evmCallStack_callHitError(18);
    }
}

public impure func arbFunctionTable_txcall() {
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        if (bytearray_size(calldata) < 4) {
            evmOp_revert_knownCodePc(address(const::Address_ArbFunctionTable), 0, 0, 0);
        }
        let funcCode = getFuncCode(calldata);
        if (funcCode == 0xce2ae159) {
            arbFunctionTable_upload(topFrame, calldata);
        } elseif(funcCode == 0x88987068) {
            arbFunctionTable_size(topFrame, calldata);
        } elseif(funcCode == 0xb464631b) {
            arbFunctionTable_get(topFrame, calldata);
        } else {
            // unrecognized function code
            evmOp_revert_knownCodePc(address(const::Address_ArbFunctionTable), 1, 0, 0);
        }
    } else {
        // this shouldn't happen -- should always be called in an EVM tx
        evmCallStack_callHitError(18);
    }
}

func getFuncCode(ba: ByteArray) -> uint {
    return asm(224, bytearray_get256(ba, 0)) uint { shr };
}

public func arbosVersion(_topFrame: EvmCallFrame) -> uint {
    return const::ArbosVersionNumber;
}

public func ArbSys_arbOSVersion(topFrame: EvmCallFrame) -> uint {
    return arbosVersion(topFrame);
}

public impure func ArbSys_arbChainID(_topFrame: EvmCallFrame) -> uint {
    return chainParams_chainId();
}

public impure func ArbSys_arbBlockNumber(_topFrame: EvmCallFrame) -> uint {
    return inbox_currentArbBlockNumber();
}

public impure func ArbSys_withdrawEth(topFrame: EvmCallFrame, destAddr: address) -> uint {
    let senderAddr = evmCallFrame_getCaller(topFrame);
    let amount = evmCallFrame_getCallvalue(topFrame);

    let runningAsAddr = evmCallFrame_runningAsAddress(topFrame);
    let runningAsAccount = evmCallFrame_getAccount(topFrame, runningAsAddr);
    if let Some(updatedAcct) = account_deductFromEthBalance(runningAsAccount, amount) {
        if ( ! evmCallStack_setAccount(runningAsAddr, updatedAcct)) {
            evmOp_revert_knownCodePc(address(const::Address_ArbSys), 31, 0, 0);
        }

        return sendTxToL1(senderAddr, destAddr, amount, bytearray_new(0));
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbSys), 33, 0, 0);
    }
    
    return 0; // should never happen
}

public impure func ArbSys_sendTxToL1(topFrame: EvmCallFrame, destAddr: address, innerCalldata: ByteArray) -> uint {
    let caller = evmCallFrame_getCaller(topFrame);
    let callvalue = evmCallFrame_getCallvalue(topFrame);
    let runningAsAddr = evmCallFrame_runningAsAddress(topFrame);
    let runningAsAccount = evmCallFrame_getAccount(topFrame, runningAsAddr);
    if let Some(updatedAcct) = account_deductFromEthBalance(runningAsAccount, callvalue) {
        if ( ! evmCallStack_setAccount(runningAsAddr, updatedAcct)) {
            evmOp_revert_knownCodePc(address(const::Address_ArbSys), 220, 0, 0);
        }

        return sendTxToL1(caller, destAddr, callvalue, innerCalldata);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbSys), 223, 0, 0);
    }
    
    return 0; // should never happen
}

impure func sendTxToL1(caller: address, destination: address, callvalue: uint, calldata: ByteArray) -> uint {
    let calldataSize = bytearray_size(calldata);
    queueMessageForSend(
        bytearray_copy(
            calldata,
            0,
            bytearray_set256(
                bytearray_set256(
                    bytearray_set256(
                        bytearray_set256(
                            bytearray_set256(
                                bytearray_set256(
                                    bytearray_setByte(
                                        bytearray_new(0),
                                        0,
                                        const::SendType_sendTxToL1
                                    ),
                                    1,
                                    uint(caller),
                                ),
                                32+1,
                                uint(destination),
                            ),
                            2*32+1,
                            inbox_currentArbBlockNumber(),
                        ),
                        3*32+1,
                        inbox_currentEthBlockNumber(),
                    ),
                    4*32+1,
                    inbox_currentTimestamp(),
                ),
                5*32+1,
                callvalue,
            ),
            6*32+1,
            calldataSize,
        ),
    );

    // write log data into memory
    let mem = bytearray_set256(
        bytearray_set256(
            bytearray_set256(
                bytearray_set256(
                    bytearray_set256(
                        bytearray_set256(
                            bytearray_set256(
                                bytearray_set256(
                                    bytearray_new(0),
                                    0,
                                    uint(caller),
                                ),
                                32,
                                0,  // item number in batch will be filled in later
                            ),
                            2*32,
                            inbox_currentArbBlockNumber(),
                        ),
                        3*32,
                        inbox_currentEthBlockNumber(),
                    ),
                    4*32,
                    inbox_currentTimestamp(),
                ),
                5*32,
                callvalue,
            ),
            6*32,
            7*32,  // offset to beginning of bytes size
        ),
        7*32,
        calldataSize,
    );

    mem = bytearray_copy(
        calldata,
        0,
        mem,
        8*32,
        32*((calldataSize+31)/32),
    );

    let savedMem = evmCallStack_getTopFrameMemoryOrDie();

    if ( ! evmCallStack_setTopFrameMemory(mem)) {
        evmOp_revert_knownCodePc(address(const::Address_ArbSys), 230, 0, 0);
    }

    let uniqueId = evmOp_sload(0);
    evmOp_sstore(0, uniqueId + 1);

    evmOp_log4(
        0,
        bytearray_size(mem),
        const::EVMLogTopicForL2ToL1Send,
        uint(destination),
        uniqueId,
        0,    // batch number will be filled in later
    );

    if ( ! evmCallStack_setTopFrameMemory(savedMem)) {
        evmOp_revert_knownCodePc(address(const::Address_ArbSys), 231, 0, 0);
    }

    return uniqueId;
}

public func ArbSys_getTransactionCount(topFrame: EvmCallFrame, addr: address) -> uint {
    return account_getNextSeqNum(evmCallFrame_getAccount(topFrame, addr));
}

public impure func ArbSys_getStorageAt(topFrame: EvmCallFrame, addr: address, offset: uint) -> uint {
    requireCalledByOwner(topFrame);

    let acct = evmCallFrame_getAccount(topFrame, addr);
    return xif let Some(val) = account_getStorageCell(acct, offset) { val } else { 0 };
}

public impure func ArbSys_isTopLevelCall(_topFrame: EvmCallFrame) -> bool {
    return (evmCallStack_stackDepth() == 2);
}

impure func arbAddressTable_register(_topFrame: EvmCallFrame, calldata: ByteArray) { // (address addr) -> uint
// return the index of addr in the address table
// if addr isn't in the table already, add it to the table and return its new index
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 110, 0, 0);
    }
    let addr = address(bytearray_get256(calldata, 4));
    let (atab, idx, changed) = addressTable_getByAddressAlloc(getGlobalAddressTable(), addr);
    if (changed) {
        evmCallStack_addStorageCharge(2);   // charge current tx for 2 storage cells
        setGlobalAddressTable(atab);
    }
    if (evmCallStack_setTopFrameMemory(bytearray_set256(evmCallStack_getTopFrameMemoryOrDie(), 0, idx))) {
        evmOp_return(0, 32);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 111, 0, 0);
    }
}

impure func arbAddressTable_lookup(_topFrame: EvmCallFrame, calldata: ByteArray) {  // (address addr) -> uint
// return the index of addr in the address table; revert if it's not in the table
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 120, 0, 0);
    }
    let addr = address(bytearray_get256(calldata, 4));
    if let Some(idx) = addressTable_getByAddress(getGlobalAddressTable(), addr) {
        if (evmCallStack_setTopFrameMemory(bytearray_set256(evmCallStack_getTopFrameMemoryOrDie(), 0, idx))) {
            evmOp_return(0, 32);
        } else {
            evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 121, 0, 0);
        }
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 122, 0, 0);
    }
}

impure func arbAddressTable_addressExists(_topFrame: EvmCallFrame, calldata: ByteArray) { // (address addr) -> bool
// return true iff addr is in the address table
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 130, 0, 0);
    }
    let addr = address(bytearray_get256(calldata, 4));
    let inTable = 0;
    if let Some(_) = addressTable_getByAddress(getGlobalAddressTable(), addr) {
        inTable = 1;
    }

    if (evmCallStack_setTopFrameMemory(bytearray_set256(evmCallStack_getTopFrameMemoryOrDie(), 0, inTable))) {
        evmOp_return(0, 32);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 131, 0, 0);
    }
}

impure func arbAddressTable_size(_topFrame: EvmCallFrame, calldata: ByteArray) { // () -> uint
// return the number of items in the address table
    if (bytearray_size(calldata) != 4) {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 140, 0, 0);
    }
    let size = addressTable_size(getGlobalAddressTable());
    if (evmCallStack_setTopFrameMemory(bytearray_set256(evmCallStack_getTopFrameMemoryOrDie(), 0, size))) {
        evmOp_return(0, 32);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 141, 0, 0);
    }
}

impure func arbAddressTable_lookupIndex(_topFrame: EvmCallFrame, calldata: ByteArray) {  // (uint index) -> address
// return the address at slot index in the address table, or revert if index is beyond the end of the table
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 150, 0, 0);
    }
    let index = bytearray_get256(calldata, 4);
    if let Some(addr) = addressTable_getByIndex(getGlobalAddressTable(), index) {
        if (evmCallStack_setTopFrameMemory(bytearray_set256(evmCallStack_getTopFrameMemoryOrDie(), 0, uint(addr)))) {
            evmOp_return(0, 32);
        } else {
            evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 151, 0, 0);
        }
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 152, 0, 0);
    }
}

impure func arbAddressTable_decompress(_topFrame: EvmCallFrame, calldata: ByteArray) {  // (bytes buf, uint offset) -> (address, uint)
    // read a compressed address from buf at offset, return (resulting address, updated offset)
    // revert if buf is too short
    if (bytearray_size(calldata) < 4+4*32) {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 160, 0, 0);
    }
    let offset = bytearray_get256(calldata, 36);
    let bytesLen = bytearray_get256(calldata, 68);
    if (offset >= bytesLen) {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 161, 0, 0);  // buffer is too short
    }
    let stream = bytestream_new(bytearray_extract(calldata, 100+offset, bytesLen-offset));
    if let Some(res) = decompressAddress(stream) {
        let (ustr, addr) = res;
        stream = ustr;
        if (evmCallStack_setTopFrameMemory(
            bytearray_set256(
                bytearray_set256(
                    evmCallStack_getTopFrameMemoryOrDie(),
                    0,
                    uint(addr)
                ),
                32,
                offset + bytestream_bytesReadSoFar(stream),
            )
        )) {
            evmOp_return(0, 64);
        } else {
            evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 162, 0, 0);
        }
    } else {
        // input was too short
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 163, 0, 0);
    }
}

impure func arbAddressTable_compress(_topFrame: EvmCallFrame, calldata: ByteArray) { // (address addr) -> bytes
// compress addr, based on the current address table contents, and return the resulting buffer
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 100, 0, 0);
    }
    let addr = address(bytearray_get256(calldata, 4));
    let compressedData = bytearray_new(0);
    let compressedDataLen = 0;
    if let Some(index) = addressTable_getByAddress(getGlobalAddressTable(), addr) {
        let (ba, sz) = rlp_encodeUint(index, compressedData, 0);
        compressedData = ba;
        compressedDataLen = sz;
    } else {
        let (ba, sz) = rlp_encodeAddress(addr, compressedData, 0);
        compressedData = ba;
        compressedDataLen = sz;
    }
    let mem = bytearray_set256(
        bytearray_set256(
            bytearray_set256(
                evmCallStack_getTopFrameMemoryOrDie(),
                0,
                32
            ),
            32,
            compressedDataLen
        ),
        64,
        bytearray_get256(compressedData, 0)
    );

    if (evmCallStack_setTopFrameMemory(mem)) {
        evmOp_return(0, 96);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbAddressTable), 101, 0, 0);
    }
}

impure func arbBLS_register(topFrame: EvmCallFrame, calldata: ByteArray) {
    if (bytearray_size(calldata) != 4+32*4) {
        evmOp_revert_knownCodePc(address(const::Address_ArbBLS), 170, 0, 0);
    }

    let x0 = bytearray_get256(calldata, 4);
    let x1 = bytearray_get256(calldata, 4+32);
    let y0 = bytearray_get256(calldata, 4+32*2);
    let y1 = bytearray_get256(calldata, 4+32*3);
    let maybeBlsKey = bls_makeKey(x0, x1, y0, y1);

    let caller = evmCallFrame_getCaller(topFrame);
    let acct = evmCallFrame_getAccount(topFrame, caller);
    if (evmCallStack_setAccount(caller, account_setBlsKey(acct, maybeBlsKey))) {
        evmOp_return(0, 0);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbBLS), 171, 0, 0);
    }
}

impure func arbBLS_getPublicKey(topFrame: EvmCallFrame, calldata: ByteArray) { // (address) -> (uint, uint, uint, uint)
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbBLS), 180, 0, 0);
    }
    let addr = address(bytearray_get256(calldata, 4));
    let maybeBlsKey = account_getBlsKey(evmCallFrame_getAccount(topFrame, addr));

    let mem = evmCallStack_getTopFrameMemoryOrDie();
    if let Some(blsKey) = maybeBlsKey {
        mem = bls_marshalPublicKey(blsKey, mem, 0);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbBLS), 101, 0, 0);
    }

    if (evmCallStack_setTopFrameMemory(mem)) {
        evmOp_return(0, 4*32);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbBLS), 182, 0, 0);
    }
}

impure func arbFunctionTable_upload(topFrame: EvmCallFrame, calldata: ByteArray) { // (bytes)
    if (bytearray_size(calldata) < (4+2*32)) {
        evmOp_revert_knownCodePc(address(const::Address_ArbFunctionTable), 190, 0, 0);
    }

    let callerAddr = evmCallFrame_getCaller(topFrame);
    let nbytes = bytearray_get256(calldata, 4+32);

    let tableData = bytearray_extract(calldata, 4+2*32, nbytes);

    let account = evmCallFrame_getAccount(topFrame, callerAddr);
    if let Some(funcTable) = parseAggregatorFunctionTable(
        tableData,
    ) {
        account = account_setAggregatorDecompressionState(
            account,
            Some(funcTable)
        );
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbFunctionTable), 191, 0, 0);
    }

    if (evmCallStack_setAccount(callerAddr, account)) {
        evmOp_return(0, 0);
    } else {
        evmOp_revert_knownCodePc(address(const::Address_ArbFunctionTable), 192, 0, 0);
    }
}

impure func arbFunctionTable_size(_topFrame: EvmCallFrame, calldata: ByteArray) { // (address) -> uint
    if (bytearray_size(calldata) != 36) {
        evmOp_revert_knownCodePc(address(const::Address_ArbFunctionTable), 200, 0, 0);
    }

    if let Some(size) = functionTableSize(address(bytearray_get256(calldata, 4))) {
        if (evmCallStack_setTopFrameMemory(bytearray_set256(bytearray_new(0), 0, size))) {
            evmOp_return(0, 32);
        }
    }

        evmOp_revert_knownCodePc(address(const::Address_ArbFunctionTable), 201, 0, 0);
}

impure func arbFunctionTable_get(_topFrame: EvmCallFrame, calldata: ByteArray) { // (address, uint) -> (uint, bool, uint)
    if (bytearray_size(calldata) != 68) {
        evmOp_revert_knownCodePc(address(const::Address_ArbFunctionTable), 210, 0, 0);
    }

    let addr = address(bytearray_get256(calldata, 4));
    let index = bytearray_get256(calldata, 36);

    if let Some(res) = getFromFunctionTable(addr, index) {
        let (funcCode, isPayable, gasLimit) = res;
        if (evmCallStack_setTopFrameMemory(
            bytearray_set256(
                bytearray_set256(
                    bytearray_set256(
                        bytearray_new(0),
                        0,
                        funcCode
                    ),
                    32,
                    uint(isPayable),
                ),
                64,
                gasLimit,
            )
        )) {
            evmOp_return(0, 96);
        }
    }

    evmOp_revert_knownCodePc(address(const::Address_ArbFunctionTable), 211, 0, 0);
}

