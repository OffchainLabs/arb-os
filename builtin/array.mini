//
// Copyright 2020, Offchain Labs, Inc. All rights reserved.
//

// This is the implementation of arrays in the Mini language.
// Array accesses in Mini get compiled into calls to the library.
// So please consult with the team before changing APIs or semantics of any of this.

// These functions can also be imported and called directly by Mini programs.

// Normal Mini code won't ever "reach inside" an array to disturb the integrity of this
// data structure. But note that use of unsafe language features could lead to structural
// damage to the internals of an array, leading to erroneous results or even a panic.


type block = [8]any    // this is basically a maximum-size AVM tuple

// An array is represented as a struct which contains a tree of blocks.
// The block-tree holds the contents of the array.
// The block-tree is a complete tree, of the minimum height needed to hold the array.
type array = struct {
	size: uint,      // number of items in the array
	topstep: uint,   // number of items in each "top-level" slot in the tree structure
	contents: block, // top-level of the tree structure
}

public func builtin_arrayNew(sz: uint, base_val: any) -> array {
	let chunk = 1;
	while (8*chunk < sz) {
		chunk = 8*chunk;
		base_val = newfixedarray(8, base_val);
	}
	return struct {
		size: sz,
		topstep: chunk,
		contents: unsafecast<block>(newfixedarray(8, base_val)),
	};
}

public func builtin_arrayGet(arr: array, index: uint) -> any {
	if (index >= arr.size) {
		panic;  // out of bounds access
	}
	let res = arr.contents;
	let chunk = arr.topstep;
	while (1 <= chunk) {
		res = unsafecast<block>(res[index/chunk]);
		index = index % chunk;
		chunk = chunk / 8;
	}
	return res;
}

public func builtin_arrayGetSafe(arr: array, index: uint) -> option<any> {
    // Return None if access is out-of-bounds, otherwise return Some(result).
	if (index >= arr.size) {
		return None;
	}
	return Some(builtin_arrayGet(arr, index));  // won't panic, because we bounds-checked
}

public func builtin_arrayGetConsecutive(arr: array, index: uint) -> (any, any) {
	// Get two consecutive items at [index] and [index+1]
	// This will be faster than two separate arrayGets, in the common case.
	if (index+1 >= arr.size) {
		panic;
	}
	return xif ((index % 8) == 7) {
		// leaves are not in same bottom-level block, do it the slow way
		(builtin_arrayGet(arr, index), builtin_arrayGet(arr, index+1),)
	} else {
		let res = arr.contents;
		let chunk = arr.topstep;
		while (8 <= chunk) {
			res = unsafecast<block>(res[index/chunk]);
			index = index % chunk;
			chunk = chunk / 8;
		}
		(res[index], res[index+1],)
	};
}

public func builtin_arrayGetConsecutiveSafe(arr: array, index: uint) -> option<(any, any)> {
    // Return Non if access is out-of-bounds, otherwise return Some((result, result'))
	if (index+1 >= arr.size) {
		return None;
	}
	return Some(builtin_arrayGetConsecutive(arr, index));
}

public func builtin_arraySet(arr: array, index: uint, value: any) -> array {
	if (index >= arr.size) {
		panic;
	}
	return arr with { contents: arraySet2(arr.contents, arr.topstep, index, value) };
}

public func builtin_arraySetSafe(arr: array, index: uint, value:any) -> option<array> {
	if (index >= arr.size) {
		return None;
	}
	return Some(builtin_arraySet(arr, index, value));
}

func arraySet2(b: block, chunkSize: uint, index: uint, value: any) -> block {
	return xif (1 == chunkSize) {
	    // at a leaf of the tree
		b with {[index] = value}
	} else {
	    // at an internal node of the tree
		b with {[index/chunkSize] = arraySet2(
			unsafecast<block>(b[index/chunkSize]),
			chunkSize / 8,
			index % chunkSize,
			value
		)}
	};
}

public func builtin_arraySwap(a: array, index: uint, value: any) -> (array, any) {
    // Write a new value into a slot of the array, returning the old value that was in that slot
	if (index >= a.size) {
		panic;
	}
	let sub = arraySwap2(a.contents, a.topstep, index, value);
	return (
		a with { contents: sub.bloc },
		sub.val,
	);
}

public func builtin_arraySwapSafe(a: array, index: uint, value: any) -> option<(array, any)> {
	if (index >= a.size) {
		return None;
	}
	let (aOut, valOut) = builtin_arraySwap(a, index, value);
	return Some((aOut, valOut));
}

type arraySwapSubResult = struct {
	bloc: block,
	val:  any,
}

func arraySwap2(b: block, chunk: uint, index: uint, value: any)  -> arraySwapSubResult {
	return xif (1 == chunk) {
		struct {
			bloc: b with {[index] = value},
			val: b[index],
		}
	} else {
		let subIndex = index/chunk;
		let sub = arraySwap2(
			unsafecast<block>(b[subIndex]),
			chunk/8,
			index%chunk,
			value
		);
		sub with { bloc: b with {[subIndex] = sub.bloc} }
	};
}

type opClosure = struct {
	f: func(any, any) -> (any, any),
	val: any,
}

public func builtin_arrayOp(a: array, index: uint, closure: opClosure) -> (array, any) {
    // Do a read-modify-write operation on a slot of the array.
    // closure defines the operation that will be done on the contents.
    // (newSlotContents, returnValue) = closure.f(closure.val, oldSlotContents)
    // This function returns (updatedArray, returnValue).
	if (index >= a.size) {
		panic;
	}
	let sub = arrayOp2(a.contents, a.topstep, index, closure);
	return (
		a with { contents: sub.bloc },
		sub.val,
	);
}

public func builtin_arrayOpSafe(a: array, index: uint, closure: opClosure) -> option<(array, any)> {
	if (index >= a.size) {
		return None;
	}
	let (aOut, valOut) = builtin_arrayOp(a, index, closure);
	return Some((aOut, valOut));
}

func arrayOp2(b: block, chunk: uint, index: uint, closure: opClosure) -> arraySwapSubResult {
	return xif (1 == chunk) {
		let (newSlotContents, returnValue) = closure.f(closure.val, b[index]);
		struct {
			bloc: b with { [index] = newSlotContents },
			val: returnValue,
		}
	} else {
		let subIndex = index/chunk;
		let sub = arrayOp2(
			unsafecast<block>(b[subIndex]),
			chunk/8,
			index%chunk,
			closure
		);
		sub with { bloc: b with {[subIndex] = sub.bloc} }
	};
}

public func builtin_arrayOpConsecutive(
	a: array, 
	index: uint, 
	closure1: opClosure, 
	closure2: opClosure
) -> (array, any, any) {
    // This is like builtin_arrayOp, except it operates on two consecutive items in the array.
    // It takes two closures, and returns the updated array and the two return values.

	//TODO: optimize the case where both items are in the same bottom-level block
	let (arr1, ret1,) = builtin_arrayOp(a, index, closure1);
	let (arr2, ret2,) = builtin_arrayOp(arr1, index+1, closure2);
	return (arr2, ret1, ret2,);
}

public func builtin_arrayOpConsecutiveSafe(
	a: array, 
	index: uint, 
	closure1: opClosure, 
	closure2: opClosure
) -> option<(array, any, any)> {
	let (arr1, ret1) = builtin_arrayOpSafe(a, index, closure1)?;
	let (arr2, ret2) = builtin_arrayOpSafe(arr1, index+1, closure2)?;
	return Some((arr2, ret1, ret2));
}

public func array_resize(a: array, newSize: uint, baseVal: any) -> array {
    // Resize an array.
    // If the new size is larger, copy over all of the old contents, and fill the added elements with baseVal.
    // If the new size is smaller, copy over all of the old contents that will fit.

	// TODO: make this more efficient by reusing more of the tree-structure of a
	let minSize = a.size;
	if (minSize > newSize) {
		minSize = newSize;
	}
	let ret = builtin_arrayNew(newSize, baseVal);
	let i = 0;
	while (i < minSize) {
		ret = builtin_arraySet(ret, i, builtin_arrayGet(a, i));
		i = i+1;
	}
	return ret;
}
