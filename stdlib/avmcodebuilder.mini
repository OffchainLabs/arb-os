//
// Copyright 2020, Offchain Labs, Inc. All rights reserved.
//

use std::bytestream::ByteStream;
use std::bytestream::bytestream_atEof;
use std::bytestream::bytestream_getByte;

use std::rlp::rlp_decodeUint;


type AvmCodePoint = view write func();

type AvmCodeBuilder = struct {
    requireSafe: bool,
    code: AvmCodePoint,
    size: uint,
    codeMap: map<uint, AvmCodePoint>,
};

public view write func pushInstruction(opcode: uint, restOfCode: AvmCodePoint) -> AvmCodePoint {
    // Push an instruction onto the beginning of a code segment, and return the resulting CodePoint.
    return asm(opcode, restOfCode) AvmCodePoint { pushinsn };
}

public view write func pushInstructionImm(opcode: uint, val: any, restOfCode: AvmCodePoint) -> AvmCodePoint {
    // Push an instruction (with immediate value) onto the beginning of a code segment, and return the resulting CodePoint.
    return asm(opcode, val, restOfCode) AvmCodePoint { pushinsnimm };
}

public view write func pushValue(val: any, restOfCode: AvmCodePoint) -> AvmCodePoint {
    // Push a noop (with immediate value) onto the beginning of a code segment, and return the resulting CodePoint.
    return asm(const::AVM_nop, val, restOfCode) AvmCodePoint { pushinsnimm };
}

public view func avmCodeBuilder_new(requireSafe: bool) -> AvmCodeBuilder {
    let code = asm() AvmCodePoint { errcodept };
    return struct {
        requireSafe: requireSafe,
        code: code,
        size: 1,
        codeMap: newmap<uint, AvmCodePoint> with { [0] = code },
    };
}

public view write func avmCodeBuilder_append(builder: AvmCodeBuilder, stream: ByteStream) -> option<AvmCodeBuilder> {
    let requireSafe = builder.requireSafe;
    let code = builder.code;
    let size = builder.size;
    let codeMap = builder.codeMap;

    while( ! bytestream_atEof(stream) ) {
        let (*stream, insn) = bytestream_getByte(stream)?;
        if ( requireSafe && !isSafeInstruction(insn) ) {
            return None;
        }
        let (*stream, hasImm) = bytestream_getByte(stream)?;
        if (hasImm == 0) {
            code = pushInstruction(insn, code);
        } else {
            let (*stream, val) = unmarshalValue(stream, codeMap)?;
            code = pushInstructionImm(insn, val, code);
        }
        set codeMap[size] = code;
        size = size+1;
    }

    return Some(struct {
        requireSafe: requireSafe,
        code: code,
        size: size,
        codeMap: codeMap,
    });
}

public func avmCodeBuilder_getCodeHash(builder: AvmCodeBuilder) -> bytes32 {
    return hash(builder.code);
}

public func avmCodeBuilder_finish(builder: AvmCodeBuilder) -> AvmCodePoint {
    return builder.code;
}

func isSafeInstruction(insn: uint) -> bool {
    return (insn != const::AVM_rset)
        && (insn != const::AVM_errset)
        && ((insn < 0x60) || (insn > 0x7f));   // various unsafe instructions
}

func unmarshalValue(stream: ByteStream, codeMap: map<uint, AvmCodePoint>) -> option<(ByteStream, any)> {
    let (*stream, kind) = bytestream_getByte(stream)?;
    return xif (kind == 0) {  // integer
        let (*stream, val) = rlp_decodeUint(stream)?;
        Some((stream, val))
    } elseif (kind == 1) { // codepoint
        let (*stream, idx) = rlp_decodeUint(stream)?;
        xif let Some(codePt) = codeMap[idx] {
            Some((stream, codePt))
        } else {
            None<(ByteStream, any)>
        }
    } elseif (kind == 2) { // empty buffer
        Some((stream, newbuffer()))
    } elseif (kind == 3) {
        let (ln,buf) = #include "rlp-nohash.wasm";
        Some((stream, buf))
    } elseif (kind == 10) {   // 0-tuple
        Some((stream, ()))
    } elseif (kind == 11) {   // 1-tuple
        let val = unsafecast<[1]any>(None<any>);
        let (*stream, x) = unmarshalValue(stream, codeMap)?;
        set val[0] = x;
        Some((stream, val))
    } elseif (kind == 12) {   // 2-tuple
        let val = unsafecast<[2]any>((0,0));
        let i = 0;
        while (i < 2) {
            let (*stream, x) = unmarshalValue(stream, codeMap)?;
            set val[i] = x;
            i = i+1;
        }
        Some((stream, val))
    } elseif (kind == 13) {   // 3-tuple
        let val = unsafecast<[3]any>((0,0,0));
        let i = 0;
        while (i < 3) {
            let (*stream, x) = unmarshalValue(stream, codeMap)?;
            set val[i] = x;
            i = i+1;
        }
        Some((stream, val))
    } elseif (kind == 14) {   // 4-tuple
        let val = unsafecast<[4]any>((0,0,0,0));
        let i = 0;
        while (i < 4) {
            let (*stream, x) = unmarshalValue(stream, codeMap)?;
            set val[i] = x;
            i = i+1;
        }
        Some((stream, val))
    } elseif (kind == 15) {   // 5-tuple
        let val = unsafecast<[5]any>((0,0,0,0,0));
        let i = 0;
        while (i < 5) {
            let (*stream, x) = unmarshalValue(stream, codeMap)?;
            set val[i] = x;
            i = i+1;
        }
        Some((stream, val))
    } elseif (kind == 16) {   // 6-tuple
        let val = unsafecast<[6]any>((0,0,0,0,0,0));
        let i = 0;
        while (i < 6) {
            let (*stream, x) = unmarshalValue(stream, codeMap)?;
            set val[i] = x;
            i = i+1;
        }
        Some((stream, val))
    } elseif (kind == 17) {   // 7-tuple
        let val = unsafecast<[7]any>((0,0,0,0,0,0,0));
        let i = 0;
        while (i < 7) {
            let (*stream, x) = unmarshalValue(stream, codeMap)?;
            set val[i] = x;
            i = i+1;
        }
        Some((stream, val))
    } elseif (kind == 18) {   // 8-tuple
        let val = unsafecast<[8]any>((0,0,0,0,0,0,0,0));
        let i = 0;
        while (i < 8) {
            let (*stream, x) = unmarshalValue(stream, codeMap)?;
            set val[i] = x;
            i = i+1;
        }
        Some((stream, val))
    } else {
        None<(ByteStream, any)>
    };
}
