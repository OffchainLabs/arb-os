//
// Copyright 2020, Offchain Labs, Inc. All rights reserved.
//

// This file implements fixed-point numbers, consisting of a numerator and denominator.
// Some operations can only be done on values that have the same denominator.
// We assume that num and denom will always be less than 2**128. If this is true, arithmetic will never overflow.

use std::safeMath::safeAdd;
use std::safeMath::safeSub;
use std::safeMath::safeMul;

type FixedPoint = struct {
    val: uint,
    shiftFactor: uint,
};

public func fixedPoint_new(val: uint, shiftFactor: uint) -> FixedPoint {
    struct {
        val: val,
        shiftFactor: shiftFactor,
    }
}

public func fixedPoint_zero() -> FixedPoint {
    fixedPoint_new(0, 1)
}

public func fixedPoint_isZero(x: FixedPoint) -> bool {
    x.val == 0
}

public throw func fixedPoint_fromUintMatch(x: uint, fp: FixedPoint) -> FixedPoint {
    fp with { val: safeMul(x, fp.shiftFactor) }
}

public func fixedPoint_getNum(x: FixedPoint) -> uint {
    x.val
}

public func fixedPoint_getDenom(x: FixedPoint) -> uint {
    x.shiftFactor
}

public throw func fixedPoint_equal(x: FixedPoint, y: FixedPoint) -> bool {
    safeMul(x.val, y.shiftFactor) == safeMul(y.val, x.shiftFactor)
}

public throw func fixedPoint_compare(x: FixedPoint, y: FixedPoint) -> int {
    let xProd = safeMul(x.val, y.shiftFactor);
    let yProd = safeMul(y.val, x.shiftFactor);
    if      xProd  > yProd {    int(1)    }
    else if xProd == yProd {    int(0)    }
    else                   {   -int(1)    }
}

public throw func fixedPoint_add(x: FixedPoint, y: FixedPoint) -> option<FixedPoint> {
    if x.shiftFactor == y.shiftFactor {
        Some(x with { val: safeAdd(x.val, y.val) })
    } else {
        None
    }
}

public throw func fixedPoint_plusOne(x: FixedPoint) -> FixedPoint {
    x with { val: safeAdd(x.val, x.shiftFactor) }
}

public func fixedPoint_minusOne(x: FixedPoint) -> option<FixedPoint> {
    if x.val >= x.shiftFactor {
        Some(fixedPoint_new(x.val - x.shiftFactor, x.shiftFactor))
    } else {
        None
    }
}

public throw func fixedPoint_sub(x: FixedPoint, y: FixedPoint) -> option<FixedPoint> {
    if x.shiftFactor == y.shiftFactor {
        Some(x with { val: safeSub(x.val, y.val) })
    } else {
        None
    }
}

public throw func fixedPoint_mul(x: FixedPoint, y: FixedPoint) -> option<FixedPoint> {
    if x.shiftFactor == y.shiftFactor {
        Some(x with { val: safeMul(x.val, y.val) / x.shiftFactor })
    } else {
        None
    }
}

public throw func fixedPoint_mulByUint(x: FixedPoint, i: uint) -> FixedPoint {
    x with { val: safeMul(x.val, i) }
}

public throw func fixedPoint_mulByUintTrunc(x: FixedPoint, i: uint) -> uint {
    safeMul(x.val, i) / x.shiftFactor
}

public throw func fixedPoint_div(x: FixedPoint, y: FixedPoint) -> option<FixedPoint> {
    if (x.shiftFactor == y.shiftFactor) && (x.shiftFactor != 0) {
        Some(fixedPoint_new(x.val, y.val))
    } else {
        let denom = safeMul(x.shiftFactor, y.val);
        if denom == 0 {
            None
        } else {
            Some(fixedPoint_new(safeMul(x.val, y.shiftFactor), denom))
        }
    }
}

public func fixedPoint_integerPart(x: FixedPoint) -> uint {
    x.val / x.shiftFactor
}

public func fixedPoint_fractionalPart(x: FixedPoint) -> FixedPoint {
    x with { val: x.val % x.shiftFactor }
}
